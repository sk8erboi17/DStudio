// Q36 Metal matvec kernels used by generation.

constant short FC_mul_mv_nsg   [[function_constant(FC_MUL_MV + 0)]];
constant short FC_mul_mv_nxpsg [[function_constant(FC_MUL_MV + 1)]];

struct q36_metal_args_mul_mv {
    int ne00;
    int ne01;
    int ne02;
    ulong nb00;
    ulong nb01;
    ulong nb02;
    ulong nb03;
    int ne10;
    int ne11;
    int ne12;
    ulong nb10;
    ulong nb11;
    ulong nb12;
    ulong nb13;
    int ne0;
    int ne1;
    int nr0;
    short r2;
    short r3;
};

struct q36_metal_args_compressor_pair_store {
    uint32_t width;
    uint32_t ratio;
    uint32_t pos;
    uint32_t ape_type;
};

struct q36_metal_args_mul_mm {
    int32_t ne00;
    int32_t ne02;
    uint64_t nb01;
    uint64_t nb02;
    uint64_t nb03;
    int32_t ne12;
    uint64_t nb10;
    uint64_t nb11;
    uint64_t nb12;
    uint64_t nb13;
    int32_t ne0;
    int32_t ne1;
    int16_t r2;
    int16_t r3;
};

struct q36_metal_args_mul_mv_ext {
    int32_t ne00;
    int32_t ne01;
    int32_t ne02;
    uint64_t nb00;
    uint64_t nb01;
    uint64_t nb02;
    uint64_t nb03;
    int32_t ne10;
    int32_t ne11;
    int32_t ne12;
    uint64_t nb10;
    uint64_t nb11;
    uint64_t nb12;
    uint64_t nb13;
    int32_t ne0;
    int32_t ne1;
    // Keep this layout byte-for-byte aligned with host q36_q8_mv_args.
    int32_t nr0;
    int16_t r2;
    int16_t r3;
};

template<short NR0>
static inline void helper_mv_reduce_and_write(
        device float * dst_f32,
        float sumf[NR0],
        const int r0,
        const int ne01,
        ushort tiisg,
        ushort sgitg,
        threadgroup char * shmem) {
    constexpr short NW = N_SIMDWIDTH;

    threadgroup float * shmem_f32[NR0];

    for (short row = 0; row < NR0; ++row) {
        shmem_f32[row] = (threadgroup float *) shmem + NW*row;

        if (sgitg == 0) {
            shmem_f32[row][tiisg] = 0.0f;
        }

        sumf[row] = simd_sum(sumf[row]);
    }

    threadgroup_barrier(mem_flags::mem_threadgroup);

    for (short row = 0; row < NR0; ++row) {
        if (tiisg == 0) {
            shmem_f32[row][sgitg] = sumf[row];
        }
    }

    threadgroup_barrier(mem_flags::mem_threadgroup);

    for (short row = 0; row < NR0 && r0 + row < ne01; ++row) {
        float tot = simd_sum(shmem_f32[row][tiisg]);

        if (tiisg == 0 && sgitg == 0) {
            dst_f32[r0 + row] = tot;
        }
    }
}

template<short NR0, typename args_t>
void kernel_mul_mv_q8_0_f32_impl(
        args_t args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem,
        uint3  tgpig,
        ushort tiisg,
        ushort sgitg) {
    const short NSG = FC_mul_mv_nsg;

    constexpr short NW = N_SIMDWIDTH;
    constexpr short NQ = 8;

    const int nb = args.ne00/QK8_0;

    const int r0 = tgpig.x*NR0;
    const int r1 = tgpig.y;
    const int im = tgpig.z;

    const uint i12 = im%args.ne12;
    const uint i13 = im/args.ne12;

    const uint64_t offset1 = r1*args.nb11 + (i12)*args.nb12 + (i13)*args.nb13;

    device const float * y = (device const float *) (src1 + offset1);

    device const block_q8_0 * ax[NR0];
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        const uint64_t offset0 = (r0 + row)*args.nb01 + (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;

        ax[row] = (device const block_q8_0 *) ((device char *) src0 + offset0);
    }

    float sumf[NR0] = { 0.f };

    const short ix = tiisg/(NW/NQ);
    const short il = tiisg%(NW/NQ);

    const int ib0 = sgitg*NQ + ix;

    float yl[NQ];

    device const float * yb = y + ib0*QK8_0 + il*NQ;

    for (int ib = ib0; ib < nb; ib += NSG*NQ) {
        for (short i = 0; i < NQ; ++i) {
            yl[i] = yb[i];
        }

        for (short row = 0; row < NR0; row++) {
            device const int8_t * qs = ax[row][ib].qs + il*NQ;

            float sumq = 0.f;
            FOR_UNROLL (short i = 0; i < NQ; ++i) {
                sumq += qs[i] * yl[i];
            }

            sumf[row] += sumq*ax[row][ib].d;
        }

        yb += NSG*NQ*QK8_0;
    }

    device float * dst_f32 = (device float *) dst + (uint64_t)im*args.ne0*args.ne1 + (uint64_t)r1*args.ne0;

    helper_mv_reduce_and_write<NR0>(
        dst_f32, sumf, r0, args.ne01, tiisg, sgitg, shmem);
}

// Decode-time Q8_0 matrix-vector multiply. Q36 uses this for Q8_0 dense
// projections such as shared experts and output-side small matvecs.
[[host_name("kernel_mul_mv_q8_0_f32")]]
kernel void kernel_mul_mv_q8_0_f32(
        constant q36_metal_args_mul_mv & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_q8_0_f32_impl<N_R0_Q8_0, constant q36_metal_args_mul_mv &>(
        args, src0, src1, dst, shmem, tgpig, tiisg, sgitg);
}

[[host_name("kernel_mul_mv_q8_0_f32_r4")]]
kernel void kernel_mul_mv_q8_0_f32_r4(
        constant q36_metal_args_mul_mv & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_q8_0_f32_impl<4, constant q36_metal_args_mul_mv &>(
        args, src0, src1, dst, shmem, tgpig, tiisg, sgitg);
}

// Output projection alias used by the optimized host dispatch.
[[host_name("kernel_mul_mv_q8_0_f32_nr4")]]
kernel void kernel_mul_mv_q8_0_f32_nr4(
        constant q36_metal_args_mul_mv & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_q8_0_f32_impl<4, constant q36_metal_args_mul_mv &>(
        args, src0, src1, dst, shmem, tgpig, tiisg, sgitg);
}

// Decode Q-A/KV pair. Both projections consume the same activation row but
// have independent weight ranges and output extents. Keep the standalone Q8_0
// lane/block traversal and two-stage reduction verbatim for each bank; only
// the activation load and threadgroup scheduling are shared.
[[host_name("kernel_mul_mv_q8_0_f32_pair")]]
kernel void kernel_mul_mv_q8_0_f32_pair(
        constant q36_metal_args_mul_mv & args0,
        constant q36_metal_args_mul_mv & args1,
        device const char * src0_a,
        device const char * src0_b,
        device const char * src1,
        device       char * dst_a,
        device       char * dst_b,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    const short NSG = FC_mul_mv_nsg;
    constexpr short NW = N_SIMDWIDTH;
    constexpr short NQ = 8;
    constexpr short NR0 = N_R0_Q8_0;

    const int nb = args0.ne00 / QK8_0;
    const int r0 = tgpig.x * NR0;
    const bool active_a = r0 < args0.ne01;
    const bool active_b = r0 < args1.ne01;

    device const float *y = (device const float *)src1;
    device const block_q8_0 *ax_a[NR0];
    device const block_q8_0 *ax_b[NR0];
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        const int out_row = r0 + row;
        ax_a[row] = active_a && out_row < args0.ne01
            ? (device const block_q8_0 *)(src0_a + (uint64_t)out_row * args0.nb01)
            : (device const block_q8_0 *)src0_a;
        ax_b[row] = active_b && out_row < args1.ne01
            ? (device const block_q8_0 *)(src0_b + (uint64_t)out_row * args1.nb01)
            : (device const block_q8_0 *)src0_b;
    }

    float suma[NR0] = { 0.f };
    float sumb[NR0] = { 0.f };

    const short ix = tiisg / (NW / NQ);
    const short il = tiisg % (NW / NQ);
    const int ib0 = sgitg * NQ + ix;
    float yl[NQ];
    device const float *yb = y + ib0 * QK8_0 + il * NQ;

    for (int ib = ib0; ib < nb; ib += NSG * NQ) {
        FOR_UNROLL (short i = 0; i < NQ; ++i) {
            yl[i] = yb[i];
        }

        FOR_UNROLL (short row = 0; row < NR0; ++row) {
            const int out_row = r0 + row;
            if (active_a && out_row < args0.ne01) {
                device const int8_t *qs = ax_a[row][ib].qs + il * NQ;
                float sumq = 0.f;
                FOR_UNROLL (short i = 0; i < NQ; ++i) {
                    sumq += qs[i] * yl[i];
                }
                suma[row] += sumq * ax_a[row][ib].d;
            }
            if (active_b && out_row < args1.ne01) {
                device const int8_t *qs = ax_b[row][ib].qs + il * NQ;
                float sumq = 0.f;
                FOR_UNROLL (short i = 0; i < NQ; ++i) {
                    sumq += qs[i] * yl[i];
                }
                sumb[row] += sumq * ax_b[row][ib].d;
            }
        }

        yb += NSG * NQ * QK8_0;
    }

    threadgroup float *shared = (threadgroup float *)shmem;
    threadgroup float *sha[NR0];
    threadgroup float *shb[NR0];
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        sha[row] = shared + NW * row;
        shb[row] = shared + NW * (NR0 + row);
        if (sgitg == 0) {
            sha[row][tiisg] = 0.0f;
            if (active_b) shb[row][tiisg] = 0.0f;
        }
        suma[row] = simd_sum(suma[row]);
        if (active_b) sumb[row] = simd_sum(sumb[row]);
    }

    threadgroup_barrier(mem_flags::mem_threadgroup);

    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        if (tiisg == 0) {
            sha[row][sgitg] = suma[row];
            if (active_b) shb[row][sgitg] = sumb[row];
        }
    }

    threadgroup_barrier(mem_flags::mem_threadgroup);

    device float *out_a = (device float *)dst_a;
    device float *out_b = (device float *)dst_b;
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        const float total_a = simd_sum(sha[row][tiisg]);
        if (tiisg == 0 && sgitg == 0) {
            const int out_row = r0 + row;
            if (active_a && out_row < args0.ne01) out_a[out_row] = total_a;
        }
        if (active_b) {
            const float total_b = simd_sum(shb[row][tiisg]);
            if (tiisg == 0 && sgitg == 0) {
                const int out_row = r0 + row;
                if (out_row < args1.ne01) out_b[out_row] = total_b;
            }
        }
    }
}

// Decode shared-expert gate/up projections followed by SwiGLU:
//
//     mid = silu(min(gate, limit)) * clamp(up, -limit, limit)
//
// Q36's shared expert uses two Q8_0 matrices with the same input row.  This
// kernel preserves the exact Q8_0 dot-product reduction shape for both
// projections, still writes gate/up for diagnostics, and derives `mid` in the
// same lane that owns the reduced output row.  The point is not to fuse two
// independent weight streams into one matmul; it is to remove the separate
// activation pass and its reread of the two 2048-wide rows.
template<short NR0, bool STORE_GATE_UP>
void kernel_q36_shared_gate_up_swiglu_q8_0_impl(
        constant q36_metal_args_mul_mv & args,
        device const char * src0_gate,
        device const char * src0_up,
        device const char * src1,
        device       char * dst_gate,
        device       char * dst_up,
        device       char * dst_mid,
        constant     float &clamp_value,
        threadgroup  char * shmem,
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    const short NSG = FC_mul_mv_nsg;
    constexpr short NW = N_SIMDWIDTH;
    constexpr short NQ = 8;

    const int nb = args.ne00 / QK8_0;
    const int r0 = tgpig.x * NR0;
    const int r1 = tgpig.y;
    const int im = tgpig.z;

    const uint i12 = im % args.ne12;
    const uint i13 = im / args.ne12;
    const uint64_t offset1 = r1 * args.nb11 + i12 * args.nb12 + i13 * args.nb13;
    device const float *y = (device const float *)(src1 + offset1);

    device const block_q8_0 *ag[NR0];
    device const block_q8_0 *au[NR0];
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        const uint64_t offset0 = (r0 + row) * args.nb01 +
                                 (i12 / args.r2) * args.nb02 +
                                 (i13 / args.r3) * args.nb03;
        ag[row] = (device const block_q8_0 *)((device const char *)src0_gate + offset0);
        au[row] = (device const block_q8_0 *)((device const char *)src0_up   + offset0);
    }

    float sumg[NR0] = { 0.f };
    float sumu[NR0] = { 0.f };

    const short ix = tiisg / (NW / NQ);
    const short il = tiisg % (NW / NQ);
    const int ib0 = sgitg * NQ + ix;
    float yl[NQ];
    device const float *yb = y + ib0 * QK8_0 + il * NQ;

    for (int ib = ib0; ib < nb; ib += NSG * NQ) {
        FOR_UNROLL (short i = 0; i < NQ; ++i) {
            yl[i] = yb[i];
        }

        FOR_UNROLL (short row = 0; row < NR0; ++row) {
            device const int8_t *qg = ag[row][ib].qs + il * NQ;
            device const int8_t *qu = au[row][ib].qs + il * NQ;

            float sg = 0.f;
            float su = 0.f;
            FOR_UNROLL (short i = 0; i < NQ; ++i) {
                sg += qg[i] * yl[i];
                su += qu[i] * yl[i];
            }

            sumg[row] += sg * ag[row][ib].d;
            sumu[row] += su * au[row][ib].d;
        }

        yb += NSG * NQ * QK8_0;
    }

    threadgroup float *shmem_f32 = (threadgroup float *)shmem;
    threadgroup float *sh_gate[NR0];
    threadgroup float *sh_up[NR0];
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        sh_gate[row] = shmem_f32 + NW * row;
        sh_up[row]   = shmem_f32 + NW * (NR0 + row);
        if (sgitg == 0) {
            sh_gate[row][tiisg] = 0.0f;
            sh_up[row][tiisg] = 0.0f;
        }
        sumg[row] = simd_sum(sumg[row]);
        sumu[row] = simd_sum(sumu[row]);
    }

    threadgroup_barrier(mem_flags::mem_threadgroup);

    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        if (tiisg == 0) {
            sh_gate[row][sgitg] = sumg[row];
            sh_up[row][sgitg] = sumu[row];
        }
    }

    threadgroup_barrier(mem_flags::mem_threadgroup);

    device float *gate_f32 = (device float *)dst_gate +
        (uint64_t)im * args.ne0 * args.ne1 + (uint64_t)r1 * args.ne0;
    device float *up_f32 = (device float *)dst_up +
        (uint64_t)im * args.ne0 * args.ne1 + (uint64_t)r1 * args.ne0;
    device float *mid_f32 = (device float *)dst_mid +
        (uint64_t)im * args.ne0 * args.ne1 + (uint64_t)r1 * args.ne0;

    FOR_UNROLL (short row = 0; row < NR0 && r0 + row < args.ne01; ++row) {
        const float gate = simd_sum(sh_gate[row][tiisg]);
        const float up = simd_sum(sh_up[row][tiisg]);
        if (tiisg == 0 && sgitg == 0) {
            const uint out_row = r0 + row;
            if (STORE_GATE_UP) {
                gate_f32[out_row] = gate;
                up_f32[out_row] = up;
            }
            float g = gate;
            float u = up;
            if (clamp_value > 1.0e-6f) {
                g = min(g, clamp_value);
                u = clamp(u, -clamp_value, clamp_value);
            }
            const float silu = g / (1.0f + exp(-g));
            mid_f32[out_row] = silu * u;
        }
    }
}

[[host_name("kernel_q36_shared_gate_up_swiglu_q8_0")]]
kernel void kernel_q36_shared_gate_up_swiglu_q8_0(
        constant q36_metal_args_mul_mv & args,
        device const char * src0_gate,
        device const char * src0_up,
        device const char * src1,
        device       char * dst_gate,
        device       char * dst_up,
        device       char * dst_mid,
        constant     float &clamp_value,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_q36_shared_gate_up_swiglu_q8_0_impl<N_R0_Q8_0, true>(
            args, src0_gate, src0_up, src1, dst_gate, dst_up, dst_mid,
            clamp_value, shmem, tgpig, tiisg, sgitg);
}

[[host_name("kernel_q36_shared_gate_up_swiglu_q8_0_r4")]]
kernel void kernel_q36_shared_gate_up_swiglu_q8_0_r4(
        constant q36_metal_args_mul_mv & args,
        device const char * src0_gate,
        device const char * src0_up,
        device const char * src1,
        device       char * dst_gate,
        device       char * dst_up,
        device       char * dst_mid,
        constant     float &clamp_value,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_q36_shared_gate_up_swiglu_q8_0_impl<4, true>(
            args, src0_gate, src0_up, src1, dst_gate, dst_up, dst_mid,
            clamp_value, shmem, tgpig, tiisg, sgitg);
}

[[host_name("kernel_q36_shared_mid_swiglu_q8_0")]]
kernel void kernel_q36_shared_mid_swiglu_q8_0(
        constant q36_metal_args_mul_mv & args,
        device const char * src0_gate,
        device const char * src0_up,
        device const char * src1,
        device       char * dst_gate,
        device       char * dst_up,
        device       char * dst_mid,
        constant     float &clamp_value,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_q36_shared_gate_up_swiglu_q8_0_impl<N_R0_Q8_0, false>(
            args, src0_gate, src0_up, src1, dst_gate, dst_up, dst_mid,
            clamp_value, shmem, tgpig, tiisg, sgitg);
}

[[host_name("kernel_q36_shared_mid_swiglu_q8_0_r4")]]
kernel void kernel_q36_shared_mid_swiglu_q8_0_r4(
        constant q36_metal_args_mul_mv & args,
        device const char * src0_gate,
        device const char * src0_up,
        device const char * src1,
        device       char * dst_gate,
        device       char * dst_up,
        device       char * dst_mid,
        constant     float &clamp_value,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_q36_shared_gate_up_swiglu_q8_0_impl<4, false>(
            args, src0_gate, src0_up, src1, dst_gate, dst_up, dst_mid,
            clamp_value, shmem, tgpig, tiisg, sgitg);
}

struct q36_shared_gate_up_decode_args {
    uint mid_dim;
    uint blocks;
    float gate_scale;
    float up_scale;
};

// Direct translation of Q36 Vulkan's decode-only shared expert. Each
// threadgroup owns one output row; four adjacent lanes consume one Q8_0 block
// and the two simdgroups are reduced once. Only the activated mid row is
// materialized.
kernel void q36_shared_gate_up_decode(
        device const block_q8_0 *gate_w [[buffer(0)]],
        device const block_q8_0 *up_w [[buffer(1)]],
        device const float *x [[buffer(2)]],
        device float *mid [[buffer(3)]],
        constant q36_shared_gate_up_decode_args &args [[buffer(4)]],
        uint tid [[thread_index_in_threadgroup]],
        uint lane [[thread_index_in_simdgroup]],
        uint sg [[simdgroup_index_in_threadgroup]],
        uint row [[threadgroup_position_in_grid]]) {
    if (row >= args.mid_dim) return;
    const uint boff = tid >> 2u;
    const uint eoff = (tid & 3u) * 8u;
    float gate_acc = 0.0f;
    float up_acc = 0.0f;
    for (uint b = boff; b < args.blocks; b += 16u) {
        device const block_q8_0 &gb = gate_w[(ulong)row * args.blocks + b];
        device const block_q8_0 &ub = up_w[(ulong)row * args.blocks + b];
        device const float *xb = x + (ulong)b * 32u + eoff;
        float gd = float(gb.d);
        float ud = float(ub.d);
        for (uint i = 0; i < 8u; i++) {
            float xv = xb[i];
            gate_acc += float(gb.qs[eoff + i]) * xv * gd;
            up_acc += float(ub.qs[eoff + i]) * xv * ud;
        }
    }
    gate_acc = simd_sum(gate_acc);
    up_acc = simd_sum(up_acc);
    threadgroup float sh_gate[2];
    threadgroup float sh_up[2];
    if (lane == 0u) {
        sh_gate[sg] = gate_acc;
        sh_up[sg] = up_acc;
    }
    threadgroup_barrier(mem_flags::mem_threadgroup);
    if (tid == 0u) {
        float g = (sh_gate[0] + sh_gate[1]) * args.gate_scale;
        float u = (sh_up[0] + sh_up[1]) * args.up_scale;
        mid[row] = (g / (1.0f + exp(-g))) * u;
    }
}

struct q36_shared_down_tail_decode_args {
    uint out_dim;
    uint blocks;
    float scale;
};

kernel void q36_shared_down_tail_decode(
        device const block_q8_0 *weights [[buffer(0)]],
        device const float *x [[buffer(1)]],
        device const float *scalar [[buffer(2)]],
        device float *out [[buffer(3)]],
        constant q36_shared_down_tail_decode_args &args [[buffer(4)]],
        uint tid [[thread_index_in_threadgroup]],
        uint lane [[thread_index_in_simdgroup]],
        uint sg [[simdgroup_index_in_threadgroup]],
        uint row [[threadgroup_position_in_grid]]) {
    if (row >= args.out_dim) return;
    const uint boff = tid >> 2u;
    const uint eoff = (tid & 3u) * 8u;
    float acc = 0.0f;
    for (uint b = boff; b < args.blocks; b += 16u) {
        device const block_q8_0 &wb = weights[(ulong)row * args.blocks + b];
        device const float *xb = x + (ulong)b * 32u + eoff;
        float d = float(wb.d);
        for (uint i = 0; i < 8u; i++)
            acc += float(wb.qs[eoff + i]) * xb[i] * d;
    }
    acc = simd_sum(acc);
    threadgroup float sh_sum[2];
    if (lane == 0u) sh_sum[sg] = acc;
    threadgroup_barrier(mem_flags::mem_threadgroup);
    if (tid == 0u) {
        float s = scalar[0];
        float z = s >= 0.0f ? exp(-s) : exp(s);
        float gate = s >= 0.0f ? 1.0f / (1.0f + z) : z / (1.0f + z);
        out[row] = fma((sh_sum[0] + sh_sum[1]) * args.scale, gate, out[row]);
    }
}

template<typename T0, typename T1, short NR0, typename args_t>
void kernel_mul_mv_t_t_impl(
        args_t args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem,
        uint3  tgpig,
        ushort tiisg,
        ushort sgitg) {
    const short NSG = FC_mul_mv_nsg;

    constexpr short NW = N_SIMDWIDTH;
    constexpr short NB = 32;
    constexpr short NF = 8;

    const int nb = args.ne00/NB;

    const int r0 = tgpig.x*NR0;
    const int r1 = tgpig.y;
    const int im = tgpig.z;

    const uint i12 = im%args.ne12;
    const uint i13 = im/args.ne12;

    const uint64_t offset1 = r1*args.nb11 + (i12)*args.nb12 + (i13)*args.nb13;

    device const T1 * y = (device const T1 *) (src1 + offset1);

    device const T0 * ax[NR0];
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        const uint64_t offset0 = (r0 + row)*args.nb01 + (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;

        ax[row] = (device const T0 *) ((device char *) src0 + offset0);
    }

    float sumf[NR0] = { 0.f };

    const short ix = tiisg/(NW/NF);
    const short il = tiisg%(NW/NF);

    const int ib0 = sgitg*NF + ix;

    T1 yl[NF];

    device const T1 * yb = y + (ib0*NB + il*NF);

    for (int ib = ib0; ib < nb; ib += NSG*NF) {
        for (short i = 0; i < NF; ++i) {
            yl[i] = yb[i];
        }

        for (short row = 0; row < NR0; row++) {
            device const T0 * xb = ax[row] + (ib*NB + il*NF);

            float sumq = 0.f;
            FOR_UNROLL (short i = 0; i < NF; ++i) {
                sumq += xb[i] * yl[i];
            }

            sumf[row] += sumq;
        }

        yb += NSG*NF*NW;
    }

    for (int i = nb*NB + sgitg*NW + tiisg; i < args.ne00; i += NW*NSG) {
        for (short row = 0; row < NR0; row++) {
            sumf[row] += ax[row][i] * y[i];
        }
    }

    device float * dst_f32 = (device float *) dst + (uint64_t)im*args.ne0*args.ne1 + (uint64_t)r1*args.ne0;

    helper_mv_reduce_and_write<NR0>(
        dst_f32, sumf, r0, args.ne01, tiisg, sgitg, shmem);
}

template<typename T0, typename T1, typename args_t>
void kernel_mul_mv_t_t_disp(
        args_t args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem,
        uint3  tgpig,
        ushort tiisg,
        ushort sgitg) {
    switch (args.nr0) {
        case 2: kernel_mul_mv_t_t_impl<T0, T1, 2, args_t>(args, src0, src1, dst, shmem, tgpig, tiisg, sgitg); break;
        case 4: kernel_mul_mv_t_t_impl<T0, T1, 4, args_t>(args, src0, src1, dst, shmem, tgpig, tiisg, sgitg); break;
    }
}

// Decode-time dense F32/F16 matrix-vector multiply. The instantiated kernels
// handle unquantized Q36 weights and activations that are already float rows.
template<typename T0, typename T1>
kernel void kernel_mul_mv_t_t(
        constant q36_metal_args_mul_mv & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_t_t_disp<T0, T1, constant q36_metal_args_mul_mv &>(args, src0, src1, dst, shmem, tgpig, tiisg, sgitg);
}

typedef decltype(kernel_mul_mv_t_t<half, half>) mul_mv_t_t;

// Host-visible dense matvec variants used by the graph for F32 and F16 weights.
template [[host_name("kernel_mul_mv_f32_f32")]] kernel mul_mv_t_t kernel_mul_mv_t_t<float, float>;
template [[host_name("kernel_mul_mv_f16_f32")]] kernel mul_mv_t_t kernel_mul_mv_t_t<half,  float>;

template<typename T0, typename T04, typename T1, typename T14, short NR0, typename args_t>
void kernel_mul_mv_t_t_4_impl(
        args_t args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem,
        uint3  tgpig,
        ushort tiisg,
        ushort sgitg) {
    const short NSG = FC_mul_mv_nsg;

    constexpr short NW = N_SIMDWIDTH;
    constexpr short NB  = 32;
    constexpr short NF  = 16;
    constexpr short NF4 = NF/4;

    const int nb = args.ne00/NB;

    const int r0 = tgpig.x*NR0;
    const int r1 = tgpig.y;
    const int im = tgpig.z;

    const uint i12 = im%args.ne12;
    const uint i13 = im/args.ne12;

    const uint64_t offset1 = r1*args.nb11 + (i12)*args.nb12 + (i13)*args.nb13;

    device const T1  * y  = (device const T1  *) (src1 + offset1);
    device const T14 * y4 = (device const T14 *) (src1 + offset1);

    device const T0  * ax [NR0];
    device const T04 * ax4[NR0];
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        const uint64_t offset0 = (r0 + row)*args.nb01 + (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;

        ax [row] = (device const T0  *) ((device char *) src0 + offset0);
        ax4[row] = (device const T04 *) ((device char *) src0 + offset0);
    }

    float sumf[NR0] = { 0.f };

    const short ix = tiisg/(NW/NF);
    const short il = tiisg%(NW/NF);

    const int ib0 = sgitg*NF + ix;

    T14 yl4[NF4];

    device const T14 * yb4 = y4 + (ib0*NB + il*NF)/4;

    for (int ib = ib0; ib < nb; ib += NSG*NF) {
        for (short i = 0; i < NF4; ++i) {
            yl4[i] = yb4[i];
        }

        for (short row = 0; row < NR0; row++) {
            device const T04 * xb4 = ax4[row] + (ib*NB + il*NF)/4;

            float sumq = 0.f;
            FOR_UNROLL (short i = 0; i < NF4; ++i) {
                sumq += dot(float4(xb4[i]), float4(yl4[i]));
            }

            sumf[row] += sumq;
        }

        yb4 += NSG*NF*NW/4;
    }

    for (int i = nb*NB + sgitg*NW + tiisg; i < args.ne00; i += NW*NSG) {
        for (short row = 0; row < NR0; row++) {
            sumf[row] += ax[row][i] * y[i];
        }
    }

    device float * dst_f32 = (device float *) dst + (uint64_t)im*args.ne0*args.ne1 + (uint64_t)r1*args.ne0;

    helper_mv_reduce_and_write<NR0>(
        dst_f32, sumf, r0, args.ne01, tiisg, sgitg, shmem);
}

template<typename T0, typename T04, typename T1, typename T14, typename args_t>
void kernel_mul_mv_t_t_4_disp(
        args_t args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem,
        uint3  tgpig,
        ushort tiisg,
        ushort sgitg) {
    switch (args.nr0) {
        case 2: kernel_mul_mv_t_t_4_impl<T0, T04, T1, T14, 2, args_t>(args, src0, src1, dst, shmem, tgpig, tiisg, sgitg); break;
        case 4: kernel_mul_mv_t_t_4_impl<T0, T04, T1, T14, 4, args_t>(args, src0, src1, dst, shmem, tgpig, tiisg, sgitg); break;
    };
}

// Vectorized dense matvec using float4/half4 loads. Q36 uses this where the
// inner dimension and alignment make vector loads cheaper than scalar lanes.
template<typename T0, typename T04, typename T1, typename T14>
kernel void kernel_mul_mv_t_t_4(
        constant q36_metal_args_mul_mv & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_t_t_4_disp<T0, T04, T1, T14, constant q36_metal_args_mul_mv &>(args, src0, src1, dst, shmem, tgpig, tiisg, sgitg);
}

typedef decltype(kernel_mul_mv_t_t_4<half, half4, half, half4>) mul_mv_t_t_4;

// Host-visible vectorized dense matvec variants for F32 and F16 weights.
template [[host_name("kernel_mul_mv_f32_f32_4")]] kernel mul_mv_t_t_4 kernel_mul_mv_t_t_4<float, float4, float, float4>;
template [[host_name("kernel_mul_mv_f16_f32_4")]] kernel mul_mv_t_t_4 kernel_mul_mv_t_t_4<half,  half4,  float, float4>;

// Q36 compressor projections always compute two same-shaped F16 matvecs from
// the same normalized activation: one for projected KV and one for pooling
// scores.  This paired variant keeps the exact dense F16 row-reduction shape
// for each matrix, but shares one dispatch and one activation stream.
template<short NR0, typename args_t>
void kernel_mul_mv_f16_f32_pair_4_impl(
        args_t args,
        device const char * src0_a,
        device const char * src0_b,
        device const char * src1,
        device       char * dst_a,
        device       char * dst_b,
        threadgroup  char * shmem,
        uint3  tgpig,
        ushort tiisg,
        ushort sgitg) {
    const short NSG = FC_mul_mv_nsg;

    constexpr short NW = N_SIMDWIDTH;
    constexpr short NB  = 32;
    constexpr short NF  = 16;
    constexpr short NF4 = NF/4;

    const int nb = args.ne00/NB;

    const int r0 = tgpig.x*NR0;
    const int r1 = tgpig.y;
    const int im = tgpig.z;

    const uint i12 = im%args.ne12;
    const uint i13 = im/args.ne12;

    const uint64_t offset1 = r1*args.nb11 + (i12)*args.nb12 + (i13)*args.nb13;

    device const float  * y  = (device const float  *) (src1 + offset1);
    device const float4 * y4 = (device const float4 *) (src1 + offset1);

    device const half  * ax_a [NR0];
    device const half4 * ax4_a[NR0];
    device const half  * ax_b [NR0];
    device const half4 * ax4_b[NR0];
    FOR_UNROLL (short row = 0; row < NR0; ++row) {
        const uint64_t offset0 = (r0 + row)*args.nb01 + (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;

        ax_a [row] = (device const half  *) ((device char *) src0_a + offset0);
        ax4_a[row] = (device const half4 *) ((device char *) src0_a + offset0);
        ax_b [row] = (device const half  *) ((device char *) src0_b + offset0);
        ax4_b[row] = (device const half4 *) ((device char *) src0_b + offset0);
    }

    float sum_a[NR0] = { 0.f };
    float sum_b[NR0] = { 0.f };

    const short ix = tiisg/(NW/NF);
    const short il = tiisg%(NW/NF);

    const int ib0 = sgitg*NF + ix;

    float4 yl4[NF4];

    device const float4 * yb4 = y4 + (ib0*NB + il*NF)/4;

    for (int ib = ib0; ib < nb; ib += NSG*NF) {
        for (short i = 0; i < NF4; ++i) {
            yl4[i] = yb4[i];
        }

        for (short row = 0; row < NR0; row++) {
            device const half4 * xb4_a = ax4_a[row] + (ib*NB + il*NF)/4;
            device const half4 * xb4_b = ax4_b[row] + (ib*NB + il*NF)/4;

            float suma = 0.f;
            float sumb = 0.f;
            FOR_UNROLL (short i = 0; i < NF4; ++i) {
                const float4 yv = float4(yl4[i]);
                suma += dot(float4(xb4_a[i]), yv);
                sumb += dot(float4(xb4_b[i]), yv);
            }

            sum_a[row] += suma;
            sum_b[row] += sumb;
        }

        yb4 += NSG*NF*NW/4;
    }

    for (int i = nb*NB + sgitg*NW + tiisg; i < args.ne00; i += NW*NSG) {
        for (short row = 0; row < NR0; row++) {
            const float yi = y[i];
            sum_a[row] += ax_a[row][i] * yi;
            sum_b[row] += ax_b[row][i] * yi;
        }
    }

    device float * dst_a_f32 = (device float *) dst_a + (uint64_t)im*args.ne0*args.ne1 + (uint64_t)r1*args.ne0;
    device float * dst_b_f32 = (device float *) dst_b + (uint64_t)im*args.ne0*args.ne1 + (uint64_t)r1*args.ne0;

    helper_mv_reduce_and_write<NR0>(
        dst_a_f32, sum_a, r0, args.ne01, tiisg, sgitg, shmem);
    threadgroup_barrier(mem_flags::mem_threadgroup);
    helper_mv_reduce_and_write<NR0>(
        dst_b_f32, sum_b, r0, args.ne01, tiisg, sgitg, shmem);
}

template<typename args_t>
void kernel_mul_mv_f16_f32_pair_4_disp(
        args_t args,
        device const char * src0_a,
        device const char * src0_b,
        device const char * src1,
        device       char * dst_a,
        device       char * dst_b,
        threadgroup  char * shmem,
        uint3  tgpig,
        ushort tiisg,
        ushort sgitg) {
    switch (args.nr0) {
        case 2: kernel_mul_mv_f16_f32_pair_4_impl<2>(args, src0_a, src0_b, src1, dst_a, dst_b, shmem, tgpig, tiisg, sgitg); break;
        case 4: kernel_mul_mv_f16_f32_pair_4_impl<4>(args, src0_a, src0_b, src1, dst_a, dst_b, shmem, tgpig, tiisg, sgitg); break;
    }
}

kernel void kernel_mul_mv_f16_f32_pair_4(
        constant q36_metal_args_mul_mv & args,
        device const char * src0_a,
        device const char * src0_b,
        device const char * src1,
        device       char * dst_a,
        device       char * dst_b,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_f16_f32_pair_4_disp<constant q36_metal_args_mul_mv &>(
            args, src0_a, src0_b, src1, dst_a, dst_b, shmem, tgpig, tiisg, sgitg);
}

// Decode compressor projection plus recurrent-state append. The paired
// matvec remains unchanged and still materializes both F32 outputs. After a
// device-memory barrier, the first NR0 threads reload those exact stored bits
// and perform the same state write and score+APE addition as
// kernel_q36_compressor_store_one.
kernel void kernel_mul_mv_f16_f32_pair_compressor_store_4(
        constant q36_metal_args_mul_mv & args,
        constant q36_metal_args_compressor_pair_store & store,
        device const char * src0_a,
        device const char * src0_b,
        device const char * src1,
        device       char * dst_a,
        device       char * dst_b,
        device const char * ape,
        device       float * state_kv,
        device       float * state_score,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig [[threadgroup_position_in_grid]],
        ushort tiitg [[thread_index_in_threadgroup]],
        ushort tiisg [[thread_index_in_simdgroup]],
        ushort sgitg [[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_f16_f32_pair_4_disp<constant q36_metal_args_mul_mv &>(
            args, src0_a, src0_b, src1, dst_a, dst_b,
            shmem, tgpig, tiisg, sgitg);

    threadgroup_barrier(mem_flags::mem_device);

    if (tiitg >= args.nr0 || store.width == 0u || store.ratio == 0u) {
        return;
    }
    const uint col = tgpig.x * (uint)args.nr0 + tiitg;
    if (col >= store.width) return;

    const uint pos_mod = store.pos % store.ratio;
    const uint dst_row = store.ratio == 4u ? store.ratio + pos_mod : pos_mod;
    const uint dst = dst_row * store.width + col;
    const uint ape_i = pos_mod * store.width + col;

    device volatile const float * projected_kv =
            (device volatile const float *)dst_a;
    device volatile const float * projected_score =
            (device volatile const float *)dst_b;
    float ape_v;
    if (store.ape_type == 1u) {
        ape_v = (float)(((device const half *)ape)[ape_i]);
    } else {
        ape_v = ((device const float *)ape)[ape_i];
    }

    state_kv[dst] = projected_kv[col];
    state_score[dst] = projected_score[col] + ape_v;
}

template<typename T0, typename T1, typename args_t>
void kernel_mul_mv_t_t_short_impl(
        args_t args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        uint3  tgpig,
        ushort tiisg) {
    const int r0 = tgpig.x*32 + tiisg;
    const int r1 = tgpig.y;
    const int im = tgpig.z;

    if (r0 >= args.ne01) {
        return;
    }

    const uint i12 = im%args.ne12;
    const uint i13 = im/args.ne12;

    const uint64_t offset0 = r0*args.nb01 + (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;

    device const T0 * x = (device const T0 *) (src0 + offset0);

    device float * dst_f32 = (device float *) dst + (uint64_t)im*args.ne0*args.ne1;

    const uint64_t offset1 = r1*args.nb11 + (i12)*args.nb12 + (i13)*args.nb13;

    device const T1 * y = (device const T1 *) (src1 + offset1);

    float res = 0.0f;

    for (int i = 0; i < args.ne00; ++i) {
        res += (float) x[i] * (float) y[i];
    }

    dst_f32[(uint64_t)r1*args.ne0 + r0] = res;
}

// Scalar fallback for short rows. It trades parallelism for lower dispatch and
// reduction overhead when Q36 asks for tiny dense matvecs.
template<typename T0, typename T1>
kernel void kernel_mul_mv_t_t_short(
        constant q36_metal_args_mul_mv & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiisg[[thread_index_in_simdgroup]]) {
    kernel_mul_mv_t_t_short_impl<T0, T1, constant q36_metal_args_mul_mv &>(
        args,
        src0,
        src1,
        dst,
        tgpig,
        tiisg);
}

typedef decltype(kernel_mul_mv_t_t_short<half, half>) mul_mv_t_t_short_t;

// Host-visible short-row dense matvec variants.
template [[host_name("kernel_mul_mv_f32_f32_short")]] kernel mul_mv_t_t_short_t kernel_mul_mv_t_t_short<float, float>;
template [[host_name("kernel_mul_mv_f16_f32_short")]] kernel mul_mv_t_t_short_t kernel_mul_mv_t_t_short<half,  float>;

template <typename type4x4>
void dequantize_f32(device const float4x4 * src, short il, thread type4x4 & reg) {
    reg = (type4x4)(*src);
}

template <typename type4x4>
void dequantize_f16(device const half4x4 * src, short il, thread type4x4 & reg) {
    reg = (type4x4)(*src);
}

template <typename type4x4>
void dequantize_q8_0(device const block_q8_0 *xb, short il, thread type4x4 & reg) {
    device const int8_t * qs = ((device const int8_t *)xb->qs);
    const float d = xb->d;

    float4x4 reg_f;

    for (int i = 0; i < 16; i++) {
        reg_f[i/4][i%4] = (qs[i + 16*il] * d);
    }

    reg = (type4x4) reg_f;
}

struct q36_dense_block_q4_0 {
    half d;
    uchar qs[16];
};

struct q36_dense_block_q4_K {
    half d;
    half dmin;
    uchar scales[12];
    uchar qs[128];
};

struct q36_dense_block_q2_K {
    uchar scales[16];
    uchar qs[64];
    half d;
    half dmin;
};

struct q36_dense_block_q3_K {
    uchar hmask[32];
    uchar qs[64];
    uchar scales[12];
    half d;
};

struct q36_dense_block_q5_K {
    half d;
    half dmin;
    uchar scales[12];
    uchar qh[32];
    uchar qs[128];
};

// Decode-only Q5_K cache layout. The original 5-bit codes are expanded once
// at model setup, while block scale/min metadata remains lossless.
struct q36_dense_block_q5_K_expanded {
    half d;
    half dmin;
    uchar scales[8];
    uchar mins[8];
    uchar qs[256];
};
static_assert(sizeof(q36_dense_block_q5_K_expanded) == 276,
              "expanded Q5_K block ABI");

struct q36_dense_block_q6_K {
    uchar ql[128];
    uchar qh[64];
    char scales[16];
    half d;
};

struct q36_dense_block_iq3_xxs {
    half d;
    uchar qs[96];
};

struct q36_dense_block_iq3_s {
    half d;
    uchar qs[64];
    uchar qh[8];
    uchar signs[32];
    uchar scales[4];
};

struct q36_dense_block_iq2_xxs {
    half d;
    ushort qs[32];
};

struct q36_dense_block_iq2_xs {
    half d;
    ushort qs[32];
    uchar scales[8];
};

struct q36_dense_block_iq2_s {
    half d;
    uchar qs[64];
    uchar qh[8];
    uchar scales[8];
};

struct q36_dense_block_iq1_s {
    half d;
    uchar qs[32];
    ushort qh[8];
};

struct q36_dense_block_iq4_nl_32 {
    half d;
    uchar qs[16];
};

struct q36_dense_block_iq4_nl {
    q36_dense_block_iq4_nl_32 sub[8];
};

struct q36_dense_block_iq4_xs {
    half d;
    ushort scales_h;
    uchar scales_l[4];
    uchar qs[128];
};

static constant char q36_dense_iq4_values[16] = {
    -127, -104, -83, -65, -49, -35, -22, -10,
       1,   13,  25,  38,  53,  69,  89, 113,
};

static inline uint q36_dense_load_u32(device const uchar *p) {
    return uint(p[0]) | (uint(p[1]) << 8) | (uint(p[2]) << 16) |
           (uint(p[3]) << 24);
}

static inline float4 q36_dense_signed_grid4(constant const uchar *grid,
                                             uchar signs, bool high) {
    const uchar4 values = *((constant const uchar4 *)(grid + (high ? 4 : 0)));
    const uchar4 masks = high ? uchar4(16, 32, 64, 128)
                              : uchar4(1, 2, 4, 8);
    const char4 positive = char4(values);
    return float4(select(positive, -positive,
                         (uchar4(signs) & masks) != uchar4(0)));
}

static inline uchar2 q36_dense_k_scale_min(device const uchar *scales,
                                            uint group) {
    if (group < 4)
        return uchar2(scales[group] & 63, scales[4 + group] & 63);
    return uchar2((scales[4 + group] & 15) |
                      ((scales[group - 4] >> 6) << 4),
                  (scales[4 + group] >> 4) |
                      ((scales[group] >> 6) << 4));
}

static inline int q36_dense_q3_scale(
        device const q36_dense_block_q3_K *xb, uint group) {
    uint lo = xb->scales[group & 7];
    lo = group < 8 ? lo & 15 : lo >> 4;
    uint hi = (xb->scales[8 + (group & 3)] >>
               (2 * (group >> 2))) & 3;
    return int(lo | (hi << 4)) - 32;
}

void dequantize_dense_q2_K_t4(
        device const q36_dense_block_q2_K *xb, short il,
        thread float4 &reg) {
    const uint idx = 4u * uint(il);
    const uint group = idx >> 4;
    const uint in_idx = idx & 127;
    const uint off = (idx >> 7) * 32 + (in_idx & 31);
    const uint shift = (in_idx >> 5) * 2;
    const uchar sc = xb->scales[group];
    const float dl = float(xb->d) * float(sc & 15);
    const float ml = float(xb->dmin) * float(sc >> 4);
    for (uint i = 0; i < 4; ++i)
        reg[i] = dl * float((xb->qs[off + i] >> shift) & 3) - ml;
}

void dequantize_dense_q3_K_t4(
        device const q36_dense_block_q3_K *xb, short il,
        thread float4 &reg) {
    const uint idx = 4u * uint(il);
    const uint half_id = idx >> 7;
    const uint group = (idx & 127) >> 4;
    const uint shift = 2 * (group >> 1);
    const uchar mask = uchar(1u << (half_id * 4 + (group >> 1)));
    const float dl = float(xb->d) *
                     float(q36_dense_q3_scale(xb, idx >> 4));
    for (uint i = 0; i < 4; ++i) {
        const uint pos = (idx & 31) + i;
        const int q = int((xb->qs[half_id * 32 + pos] >> shift) & 3) -
                      ((xb->hmask[pos] & mask) ? 0 : 4);
        reg[i] = dl * float(q);
    }
}

void dequantize_dense_q5_K_t4(
        device const q36_dense_block_q5_K *xb, short il,
        thread float4 &reg) {
    const uint idx = 4u * uint(il);
    const uint group = idx >> 5;
    const uint pos = idx & 31;
    const uchar2 sm = q36_dense_k_scale_min(xb->scales, group);
    const float dl = float(xb->d) * float(sm[0]);
    const float ml = float(xb->dmin) * float(sm[1]);
    for (uint i = 0; i < 4; ++i) {
        const uchar packed = xb->qs[(group >> 1) * 32 + pos + i];
        const uint lo = (group & 1) ? packed >> 4 : packed & 15;
        const uint hi = (xb->qh[pos + i] >> group) & 1;
        reg[i] = dl * float(lo | (hi << 4)) - ml;
    }
}

kernel void q36_repack_q5_K_expanded(
        device const q36_dense_block_q5_K *src [[buffer(0)]],
        device q36_dense_block_q5_K_expanded *dst [[buffer(1)]],
        constant uint &n_blocks [[buffer(2)]],
        uint ib [[thread_position_in_grid]]) {
    if (ib >= n_blocks) return;
    device const q36_dense_block_q5_K *s = src + ib;
    device q36_dense_block_q5_K_expanded *d = dst + ib;
    d->d = s->d;
    d->dmin = s->dmin;
    for (uint group = 0; group < 8; ++group) {
        const uchar2 sm = q36_dense_k_scale_min(s->scales, group);
        d->scales[group] = sm[0];
        d->mins[group] = sm[1];
        for (uint pos = 0; pos < 32; ++pos) {
            const uchar packed = s->qs[(group >> 1) * 32 + pos];
            const uint lo = (group & 1) ? packed >> 4 : packed & 15;
            const uint hi = (s->qh[pos] >> group) & 1;
            d->qs[group * 32 + pos] = uchar(lo | (hi << 4));
        }
    }
}

void dequantize_dense_iq2_xxs_t4(
        device const q36_dense_block_iq2_xxs *xb, short il,
        thread float4 &reg) {
    const uint idx = 4u * uint(il);
    const uint group = idx >> 5;
    const uint l = (idx & 31) >> 3;
    const bool high = ((idx >> 2) & 1) != 0;
    device const uchar *q = (device const uchar *)xb->qs + 8 * group;
    const uint packed = q36_dense_load_u32(q + 4);
    const uchar entry = q[l];
    const uchar signs = q36_dense_ksigns_iq3[(packed >> (7 * l)) & 127];
    const float dl = float(xb->d) * (0.5f + float(packed >> 28)) * 0.25f;
    reg = dl * q36_dense_signed_grid4(
        q36_dense_iq2xxs_grid + 8u * uint(entry), signs, high);
}

void dequantize_dense_iq2_xs_t4(
        device const q36_dense_block_iq2_xs *xb, short il,
        thread float4 &reg) {
    const uint idx = 4u * uint(il);
    const uint group = idx >> 5;
    const uint l = (idx & 31) >> 3;
    const bool high = ((idx >> 2) & 1) != 0;
    const ushort qs = xb->qs[4 * group + l];
    const uchar sc = xb->scales[group];
    const uint nibble = l < 2 ? sc & 15 : sc >> 4;
    const float dl = float(xb->d) * (0.5f + float(nibble)) * 0.25f;
    const uchar signs = q36_dense_ksigns_iq3[qs >> 9];
    constant const uchar *grid =
        (constant const uchar *)(q36_dense_iq2xs_grid + (qs & 511));
    reg = dl * q36_dense_signed_grid4(grid, signs, high);
}

void dequantize_dense_iq2_s_t4(
        device const q36_dense_block_iq2_s *xb, short il,
        thread float4 &reg) {
    const uint idx = 4u * uint(il);
    const uint group = idx >> 5;
    const uint l = (idx & 31) >> 3;
    const bool high = ((idx >> 2) & 1) != 0;
    const uint entry = xb->qs[4 * group + l] |
        ((uint(xb->qh[group]) << (8 - 2 * l)) & 0x300);
    const uchar signs = xb->qs[32 + 4 * group + l];
    const uchar sc = xb->scales[group];
    const uint nibble = l < 2 ? sc & 15 : sc >> 4;
    const float dl = float(xb->d) * (0.5f + float(nibble)) * 0.25f;
    constant const uchar *grid =
        (constant const uchar *)(q36_dense_iq2s_grid + entry);
    reg = dl * q36_dense_signed_grid4(grid, signs, high);
}

void dequantize_dense_iq1_s_t4(
        device const q36_dense_block_iq1_s *xb, short il,
        thread float4 &reg) {
    const uint idx = 4u * uint(il);
    const uint group = idx >> 5;
    const uint l = (idx & 31) >> 3;
    const bool high = ((idx >> 2) & 1) != 0;
    const ushort qh = xb->qh[group];
    const uint entry = xb->qs[4 * group + l] |
        (((uint(qh) >> (3 * l)) & 7) << 8);
    const float dl = float(xb->d) *
                     float(2 * ((uint(qh) >> 12) & 7) + 1);
    const float delta = (qh & 0x8000) ? -0.125f : 0.125f;
    constant const char *grid =
        (constant const char *)(q36_dense_iq1s_grid + entry) +
        (high ? 4 : 0);
    reg = dl * (float4(*((constant const char4 *)grid)) + delta);
}

void dequantize_dense_iq4_nl_t4(
        device const q36_dense_block_iq4_nl *xb, short il,
        thread float4 &reg) {
    device const q36_dense_block_iq4_nl_32 *sub = &xb->sub[uint(il) / 8];
    const uint chunk = uint(il) & 7;
    const bool high = chunk >= 4;
    const uint pos = 4 * (chunk & 3);
    const float d = float(sub->d);
    for (uint i = 0; i < 4; ++i) {
        const uchar packed = sub->qs[pos + i];
        reg[i] = d * float(q36_dense_iq4_values[
            high ? packed >> 4 : packed & 15]);
    }
}

void dequantize_dense_iq4_xs_t4(
        device const q36_dense_block_iq4_xs *xb, short il,
        thread float4 &reg) {
    const uint idx = 4u * uint(il);
    const uint group = idx >> 5;
    const uint pos = idx & 15;
    const bool high = (idx & 31) >= 16;
    const int ls = int((xb->scales_l[group / 2] >> (4 * (group & 1))) & 15) |
                   int(((xb->scales_h >> (2 * group)) & 3) << 4);
    const float dl = float(xb->d) * float(ls - 32);
    for (uint i = 0; i < 4; ++i) {
        const uchar packed = xb->qs[16 * group + pos + i];
        reg[i] = dl * float(q36_dense_iq4_values[
            high ? packed >> 4 : packed & 15]);
    }
}

#define Q36_DENSE_DEQ16(name, block_t, deq_t4) \
template <typename type4x4> \
void name(device const block_t *xb, short il, thread type4x4 &reg) { \
    float4x4 tmp; \
    for (short row = 0; row < 4; ++row) \
        deq_t4(xb, 4 * il + row, tmp[row]); \
    reg = (type4x4)tmp; \
}

Q36_DENSE_DEQ16(dequantize_dense_q2_K, q36_dense_block_q2_K,
                dequantize_dense_q2_K_t4)
Q36_DENSE_DEQ16(dequantize_dense_q3_K, q36_dense_block_q3_K,
                dequantize_dense_q3_K_t4)
Q36_DENSE_DEQ16(dequantize_dense_q5_K, q36_dense_block_q5_K,
                dequantize_dense_q5_K_t4)
Q36_DENSE_DEQ16(dequantize_dense_iq2_xxs, q36_dense_block_iq2_xxs,
                dequantize_dense_iq2_xxs_t4)
Q36_DENSE_DEQ16(dequantize_dense_iq2_xs, q36_dense_block_iq2_xs,
                dequantize_dense_iq2_xs_t4)
Q36_DENSE_DEQ16(dequantize_dense_iq2_s, q36_dense_block_iq2_s,
                dequantize_dense_iq2_s_t4)
Q36_DENSE_DEQ16(dequantize_dense_iq1_s, q36_dense_block_iq1_s,
                dequantize_dense_iq1_s_t4)
Q36_DENSE_DEQ16(dequantize_dense_iq4_nl, q36_dense_block_iq4_nl,
                dequantize_dense_iq4_nl_t4)
Q36_DENSE_DEQ16(dequantize_dense_iq4_xs, q36_dense_block_iq4_xs,
                dequantize_dense_iq4_xs_t4)

#undef Q36_DENSE_DEQ16

static inline uchar2 q36_dense_q4_K_scale_min(int j, int k, device const uchar *q) {
    return j < 4 ? uchar2{uchar(q[j + 0 + k] & 63), uchar(q[j + 4 + k] & 63)}
                 : uchar2{uchar((q[j + 4 + k] & 0x0f) | ((q[j - 4 + k] & 0xc0) >> 2)),
                          uchar((q[j + 4 + k] >> 4) | ((q[j - 0 + k] & 0xc0) >> 2))};
}

template <typename type4x4>
void dequantize_dense_q4_0(device const q36_dense_block_q4_0 *xb, short il, thread type4x4 &reg) {
    float4x4 reg_f;
    const float d = (float)xb->d;
    const int base = 16 * (int)il;
    for (int i = 0; i < 16; i++) {
        const int k = base + i;
        /* ggml Q4_0: elems 0..15 = low nibbles of qs[0..15], 16..31 = high. */
        const uchar packed = xb->qs[(uint)(k & 15)];
        const uchar q = (k < 16) ? (packed & 0x0f) : (packed >> 4);
        reg_f[i / 4][i % 4] = d * ((float)q - 8.0f);
    }
    reg = (type4x4)reg_f;
}

template <typename type4x4>
void dequantize_dense_q4_K(device const q36_dense_block_q4_K *xb, short il, thread type4x4 &reg) {
    device const uchar *q = xb->qs;
    short is = (il / 4) * 2;
    q = q + (il / 4) * 32 + 16 * (il & 1);
    il = il & 3;
    const uchar2 sc = q36_dense_q4_K_scale_min(is, il / 2, xb->scales);
    const float d = il < 2 ? (float)xb->d : (float)xb->d * (1.0f / 16.0f);
    const float min = (float)xb->dmin;
    const float dl = d * sc[0];
    const float ml = min * sc[1];
    const ushort mask = il < 2 ? 0x0F : 0xF0;

    float4x4 reg_f;
    for (int i = 0; i < 16; ++i) {
        reg_f[i / 4][i % 4] = dl * (q[i] & mask) - ml;
    }
    reg = (type4x4)reg_f;
}

template <typename type4x4>
void dequantize_dense_q6_K(device const q36_dense_block_q6_K *xb, short il,
                           thread type4x4 &reg) {
    const float d_all = (float)xb->d;
    device const ushort *ql = (device const ushort *)xb->ql;
    device const ushort *qh = (device const ushort *)xb->qh;
    device const char *scales = xb->scales;

    ql += 32*(il/8) + 16*((il/2)&1) + 8*(il&1);
    qh += 16*(il/8) + 8*(il&1);
    const float sc = scales[(il%2) + 2*(il/2)];
    il = (il/2) & 3;

    const uint kmask1 = il > 1 ? (il > 2 ? 0xC0C0C0C0u : 0x30303030u)
                               : (il > 0 ? 0x0C0C0C0Cu : 0x03030303u);
    const uint kmask2 = il > 1 ? 0xF0F0F0F0u : 0x0F0F0F0Fu;
    const float ml = d_all * sc * 32.0f;
    const float dl0 = d_all * sc;
    const float dl1 = dl0 / 256.0f;
    const float dl2 = dl1 / 256.0f;
    const float dl3 = dl2 / 256.0f;
    const uchar shr_h = il > 2 ? 2 : 0;
    const uchar shl_h = il > 1 ? 0 : (il > 0 ? 2 : 4);
    const uchar shr_l = il > 1 ? 4 : 0;
    float4x4 out;
    for (int i = 0; i < 4; ++i) {
        const uint low = (ql[2*i] | (uint(ql[2*i+1]) << 16)) & kmask2;
        const uint high = (qh[2*i] | (uint(qh[2*i+1]) << 16)) & kmask1;
        const uint q = ((high << shl_h) >> shr_h) | (low >> shr_l);
        out[i][0] = dl0 * float(q & 0xFFu) - ml;
        out[i][1] = dl1 * float(q & 0xFF00u) - ml;
        out[i][2] = dl2 * float(q & 0xFF0000u) - ml;
        out[i][3] = dl3 * float(q & 0xFF000000u) - ml;
    }
    reg = (type4x4)out;
}

static inline float4 q36_dense_iq3_grid4(constant const uchar *grid,
                                          uchar signs, bool high) {
    const uchar4 values = *((constant const uchar4 *)grid);
    const uchar4 masks = high ? uchar4(16, 32, 64, 128)
                              : uchar4(1, 2, 4, 8);
    const char4 positive = char4(values);
    return float4(select(positive, -positive,
                         (uchar4(signs) & masks) != uchar4(0)));
}

template <typename type4x4>
void dequantize_dense_iq3_xxs(device const q36_dense_block_iq3_xxs *xb,
                              short il, thread type4x4 &reg) {
    const int ib32 = il / 2;
    il &= 1;
    device const uchar *q3 = xb->qs + 8*ib32;
    device const ushort *gas =
        (device const ushort *)(xb->qs + QK_K/4) + 2*ib32;
    const uint aux = gas[0] | (uint(gas[1]) << 16);
    const float dl = (float)xb->d * (0.5f + float(aux >> 28)) * 0.5f;
    float4x4 out;
    constant const uchar *g1 =
        (constant const uchar *)(q36_dense_iq3xxs_grid + q3[4*il + 0]);
    constant const uchar *g2 =
        (constant const uchar *)(q36_dense_iq3xxs_grid + q3[4*il + 1]);
    uchar signs = q36_dense_ksigns_iq3[(aux >> (14*il)) & 127u];
    out[0] = dl * q36_dense_iq3_grid4(g1, signs, false);
    out[1] = dl * q36_dense_iq3_grid4(g2, signs, true);
    g1 = (constant const uchar *)(q36_dense_iq3xxs_grid + q3[4*il + 2]);
    g2 = (constant const uchar *)(q36_dense_iq3xxs_grid + q3[4*il + 3]);
    signs = q36_dense_ksigns_iq3[(aux >> (14*il + 7)) & 127u];
    out[2] = dl * q36_dense_iq3_grid4(g1, signs, false);
    out[3] = dl * q36_dense_iq3_grid4(g2, signs, true);
    reg = (type4x4)out;
}

template <typename type4x4>
void dequantize_dense_iq3_s(device const q36_dense_block_iq3_s *xb,
                            short il, thread type4x4 &reg) {
    const int ib32 = il / 2;
    il &= 1;
    device const uchar *qs = xb->qs + 8*ib32;
    device const uchar *signs = xb->signs + 4*ib32 + 2*il;
    const uchar qh = xb->qh[ib32] >> (4*il);
    const float dl = (float)xb->d *
        float(1 + 2*((xb->scales[ib32/2] >> (4*(ib32%2))) & 0xf));
    float4x4 out;
    constant const uchar *g1 = (constant const uchar *)(q36_dense_iq3s_grid +
        (qs[4*il + 0] | ((uint(qh) << 8) & 256u)));
    constant const uchar *g2 = (constant const uchar *)(q36_dense_iq3s_grid +
        (qs[4*il + 1] | ((uint(qh) << 7) & 256u)));
    out[0] = dl * q36_dense_iq3_grid4(g1, signs[0], false);
    out[1] = dl * q36_dense_iq3_grid4(g2, signs[0], true);
    g1 = (constant const uchar *)(q36_dense_iq3s_grid +
        (qs[4*il + 2] | ((uint(qh) << 6) & 256u)));
    g2 = (constant const uchar *)(q36_dense_iq3s_grid +
        (qs[4*il + 3] | ((uint(qh) << 5) & 256u)));
    out[2] = dl * q36_dense_iq3_grid4(g1, signs[1], false);
    out[3] = dl * q36_dense_iq3_grid4(g2, signs[1], true);
    reg = (type4x4)out;
}

/*
 * Bit-identical twin of dequantize_q8_0 for the MPP staging loop: same
 * half(float(qs[i]) * d) per element, but the 16 consecutive int8 lanes are
 * fetched as eight aligned 16-bit loads instead of sixteen byte loads.
 * xb->qs is always 2-byte aligned (block_q8_0.d is a half).
 */
void dequantize_q8_0_pairs(device const block_q8_0 *xb, short il, thread half4x4 & reg) {
    device const ushort *qs16 = (device const ushort *)(xb->qs + 16*il);
    const float d = xb->d;

    float4x4 reg_f;

    FOR_UNROLL (short i = 0; i < 8; i++) {
        const ushort u = qs16[i];
        reg_f[i/2][(i%2)*2 + 0] = ((float)(int8_t)(u & 0xFF)) * d;
        reg_f[i/2][(i%2)*2 + 1] = ((float)(int8_t)(u >> 8)) * d;
    }

    reg = (half4x4) reg_f;
}

template <typename type4>
void dequantize_q8_0_t4(device const block_q8_0 *xb, short il, thread type4 & reg) {
    device const int8_t * qs = ((device const int8_t *)xb->qs);
    const float d = xb->d;

    for (int i = 0; i < 4; i++) {
        reg[i] = (qs[4*(il%4) + i + 16*(il/4)] * d);
    }
}

template <typename type4>
void dequantize_dense_q4_0_t4(device const q36_dense_block_q4_0 *xb, short il, thread type4 &reg) {
    const float d = (float)xb->d;
    const int base = 4 * (int)il;
    for (int i = 0; i < 4; i++) {
        const int k = base + i;
        /* ggml Q4_0: elems 0..15 = low nibbles of qs[0..15], 16..31 = high. */
        const uchar packed = xb->qs[(uint)(k & 15)];
        const uchar q = (k < 16) ? (packed & 0x0f) : (packed >> 4);
        reg[i] = d * ((float)q - 8.0f);
    }
}

template <typename type4>
void dequantize_dense_q4_K_t4(device const q36_dense_block_q4_K *xb, short il, thread type4 &reg) {
    const short group = il / 4;
    const short row = il & 3;
    const short is = (group / 4) * 2;
    const short sub = group & 3;
    device const uchar *q = xb->qs + (group / 4) * 32 +
                            16 * (group & 1) + 4 * row;
    const uchar2 sc =
        q36_dense_q4_K_scale_min(is, sub / 2, xb->scales);
    const float d = sub < 2 ? (float)xb->d
                            : (float)xb->d * (1.0f / 16.0f);
    const float dl = d * sc[0];
    const float ml = (float)xb->dmin * sc[1];
    const ushort mask = sub < 2 ? 0x0F : 0xF0;
    for (int i = 0; i < 4; i++) reg[i] = dl * (q[i] & mask) - ml;
}

template <typename block_t,
          void (*deq)(device const block_t *, short, thread float4x4 &)>
void dequantize_dense_256_t4(device const block_t *xb, short il,
                             thread float4 &reg) {
    float4x4 tmp;
    deq(xb, il / 4, tmp);
    reg = tmp[il & 3];
}

void dequantize_dense_iq3_xxs_t4(
        device const q36_dense_block_iq3_xxs *xb, short il,
        thread float4 &reg) {
    const int ib32 = il / 8;
    const int half32 = (il / 4) & 1;
    const int row = il & 3;
    device const uchar *q3 = xb->qs + 8*ib32;
    device const ushort *gas =
        (device const ushort *)(xb->qs + QK_K/4) + 2*ib32;
    const uint aux = gas[0] | (uint(gas[1]) << 16);
    const float dl = (float)xb->d * (0.5f + float(aux >> 28)) * 0.5f;
    constant const uchar *grid = (constant const uchar *)(
        q36_dense_iq3xxs_grid + q3[4*half32 + row]);
    const uint sign_shift = 14*half32 + 7*(row/2);
    const uchar signs = q36_dense_ksigns_iq3[(aux >> sign_shift) & 127u];
    reg = dl * q36_dense_iq3_grid4(grid, signs, bool(row & 1));
}

void dequantize_dense_iq3_s_t4(
        device const q36_dense_block_iq3_s *xb, short il,
        thread float4 &reg) {
    const int ib32 = il / 8;
    const int half32 = (il / 4) & 1;
    const int row = il & 3;
    device const uchar *qs = xb->qs + 8*ib32;
    device const uchar *signs = xb->signs + 4*ib32 + 2*half32;
    const uchar qh = xb->qh[ib32] >> (4*half32);
    const uint grid_index = qs[4*half32 + row] |
        ((uint(qh) << (8 - row)) & 256u);
    constant const uchar *grid =
        (constant const uchar *)(q36_dense_iq3s_grid + grid_index);
    const float dl = (float)xb->d *
        float(1 + 2*((xb->scales[ib32/2] >> (4*(ib32%2))) & 0xf));
    const uchar sign_bits = signs[row/2];
    reg = dl * q36_dense_iq3_grid4(grid, sign_bits, bool(row & 1));
}

// Q36 small-batch mat-vec kernel used for 2..8 prompt tokens.
template<short r1ptg, typename q_t, short chpb, void (*deq_t4)(device const q_t *, short, thread float4 &) >
void kernel_mul_mv_ext_q4_f32_impl(
        constant q36_metal_args_mul_mv_ext & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        uint3   tgpig[[threadgroup_position_in_grid]],
        ushort  tiisg[[thread_index_in_simdgroup]],
        ushort  sgitg[[simdgroup_index_in_threadgroup]]) {
    const short NSG   = FC_mul_mv_nsg;
    const short nxpsg = FC_mul_mv_nxpsg;

    const short chpt = 4; // chunks per thread

    const short nypsg = (32/nxpsg);

    const short tx = tiisg%nxpsg;
    const short ty = tiisg/nxpsg;

    const int i01 = tgpig.x*(nypsg*NSG) + nypsg*sgitg + ty;
    const int i11 = tgpig.y*r1ptg;
    const int i1m = tgpig.z;

    const int i12 = i1m%args.ne12;
    const int i13 = i1m/args.ne12;

    const uint64_t offset0 = i01*args.nb01 + (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;
    const uint64_t offset1 = i11*args.nb11 + (i12        )*args.nb12 + (i13        )*args.nb13;

    device const q_t * xq = (i01 < args.ne01) ? (device const q_t *) (src0 + offset0) + tx/chpb : (device const q_t *) src0;

    device const float4 * y4[r1ptg];

    for (int ir1 = 0; ir1 < r1ptg; ++ir1) {
        y4[ir1] = (i11 + ir1 < args.ne11) ? (device const float4 *) (src1 + offset1 + ir1*args.nb11) + tx : (device const float4 *) src1;
    }

    float sumf[r1ptg] = { [ 0 ... r1ptg - 1 ] = 0.0f };

    short cch = tx%chpb; // current chunk index

    for (int ich = tx; 4*ich < args.ne00; ich += chpt*nxpsg) {
        float4 lx[chpt];

#pragma unroll(chpt)
        for (short ch = 0; ch < chpt; ++ch) {
            deq_t4(xq, cch, lx[ch]);

            cch += nxpsg;
            if (cch >= chpb) {
                xq  += cch/chpb;
                cch %= chpb;
            }
        }

#pragma unroll(chpt)
        for (short ch = 0; ch < chpt; ++ch) {
#pragma unroll(r1ptg)
            for (short ir1 = 0; ir1 < r1ptg; ++ir1) {
                sumf[ir1] += dot(lx[ch], y4[ir1][ch*nxpsg]);
            }
        }

#pragma unroll(r1ptg)
        for (short ir1 = 0; ir1 < r1ptg; ++ir1) {
            y4[ir1] += chpt*nxpsg;
        }
    }

    // reduce only the threads in each row
    for (short ir1 = 0; ir1 < r1ptg; ++ir1) {
        if (nxpsg >= 32) {
            sumf[ir1] += simd_shuffle_down(sumf[ir1], 16);
        }
        if (nxpsg >= 16) {
            sumf[ir1] += simd_shuffle_down(sumf[ir1],  8);
        }
        if (nxpsg >= 8) {
            sumf[ir1] += simd_shuffle_down(sumf[ir1],  4);
        }
        if (nxpsg >= 4) {
            sumf[ir1] += simd_shuffle_down(sumf[ir1],  2);
        }
        if (nxpsg >= 2) {
            sumf[ir1] += simd_shuffle_down(sumf[ir1],  1);
        }
    }

    if (tx == 0) {
        for (short ir1 = 0; ir1 < r1ptg && i11 + ir1 < args.ne11; ++ir1) {
            device float * dst_f32 = (device float *) dst + (uint64_t)i1m*args.ne0*args.ne1 + (uint64_t)(i11 + ir1)*args.ne0;

            if (i01 < args.ne01) {
                dst_f32[i01] = sumf[ir1];
            }
        }
    }
}

// Small-batch prompt matvec for 2..5 tokens. It bridges decode-style matvec and
// full matmul when Q36 prefill chunks are too small to amortize matrix tiles.
template<short r1ptg, typename q_t, short epb, void (*deq_t4)(device const q_t *, short, thread float4 &)>
kernel void kernel_mul_mv_ext_q4_f32_disp(
        constant q36_metal_args_mul_mv_ext & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        uint3   tgpig[[threadgroup_position_in_grid]],
        ushort  tiisg[[thread_index_in_simdgroup]],
        ushort  sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_ext_q4_f32_impl<r1ptg, q_t, epb/4, deq_t4>(args, src0, src1, dst, tgpig, tiisg, sgitg);
}

// Dense single-token decode follows the layout used by llama.cpp's IQ
// matvecs: one SIMD group owns several adjacent output rows.  The activation
// float4 is consequently loaded once and reused across all four rows, while
// each row keeps an independent accumulator.  Two SIMD groups give one
// threadgroup eight rows without adding threadgroup storage.
template<short rows_per_sg, typename q_t, short epb,
         void (*deq_t4)(device const q_t *, short, thread float4 &)>
kernel void kernel_mul_mv_rows_q4_f32_disp(
        constant q36_metal_args_mul_mv_ext & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        uint3   tgpig[[threadgroup_position_in_grid]],
        ushort  tiisg[[thread_index_in_simdgroup]],
        ushort  sgitg[[simdgroup_index_in_threadgroup]]) {
    const short NSG = FC_mul_mv_nsg;
    const int first_row = (tgpig.x * NSG + sgitg) * rows_per_sg;
    const int chunks_per_block = epb / 4;
    const int total_chunks = args.ne00 / 4;
    device const float4 * y4 = (device const float4 *)src1;
    float sum[rows_per_sg] = { [0 ... rows_per_sg - 1] = 0.0f };

    for (int ich = tiisg; ich < total_chunks; ich += 32) {
        const float4 vy = y4[ich];
        const int block = ich / chunks_per_block;
        const short chunk = ich % chunks_per_block;
#pragma unroll(rows_per_sg)
        for (short row = 0; row < rows_per_sg; ++row) {
            if (first_row + row < args.ne01) {
                device const q_t * xq = (device const q_t *)(
                    src0 + (uint64_t)(first_row + row) * args.nb01) + block;
                float4 vx;
                deq_t4(xq, chunk, vx);
                sum[row] += dot(vx, vy);
            }
        }
    }

#pragma unroll(rows_per_sg)
    for (short row = 0; row < rows_per_sg; ++row) {
        float total = simd_sum(sum[row]);
        if (tiisg == 0 && first_row + row < args.ne01)
            ((device float *)dst)[first_row + row] = total;
    }
}

static inline float4 q36_dense_iq3_grid4_tg(
        threadgroup const uint *grid, uint index, uchar signs, bool high) {
    const uchar4 values = as_type<uchar4>(grid[index]);
    const uchar4 masks = high ? uchar4(16, 32, 64, 128)
                              : uchar4(1, 2, 4, 8);
    const char4 positive = char4(values);
    return float4(select(positive, -positive,
                         (uchar4(signs) & masks) != uchar4(0)));
}

kernel void kernel_mul_mv_rows_iq3_xxs_tg_f32(
        constant q36_metal_args_mul_mv_ext & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3   tgpig[[threadgroup_position_in_grid]],
        ushort  tiisg[[thread_index_in_simdgroup]],
        ushort  sgitg[[simdgroup_index_in_threadgroup]]) {
    const short NSG = FC_mul_mv_nsg;
    const ushort lid = 32 * sgitg + tiisg;
    threadgroup uint *grid = (threadgroup uint *)shmem;
    threadgroup uchar *sign_lut = (threadgroup uchar *)(grid + 256);
    for (ushort i = lid; i < 256; i += 32 * NSG)
        grid[i] = q36_dense_iq3xxs_grid[i];
    for (ushort i = lid; i < 128; i += 32 * NSG)
        sign_lut[i] = q36_dense_ksigns_iq3[i];
    threadgroup_barrier(mem_flags::mem_threadgroup);

    const int first_row = (tgpig.x * NSG + sgitg) * 4;
    const int blocks = args.ne00 / 256;
    device const float4 * y4 = (device const float4 *)src1;
    float sum[4] = {0.0f};
    for (int block = 0; block < blocks; ++block) {
        const float4 vy[2] = {
            y4[block * 64 + tiisg], y4[block * 64 + 32 + tiisg]
        };
#pragma unroll(4)
        for (short row_out = 0; row_out < 4; ++row_out) {
            if (first_row + row_out >= args.ne01) continue;
            device const q36_dense_block_iq3_xxs *xb =
                (device const q36_dense_block_iq3_xxs *)(
                    src0 + (uint64_t)(first_row + row_out) * args.nb01) + block;
#pragma unroll(2)
            for (short part = 0; part < 2; ++part) {
                const short il = tiisg + 32 * part;
                const int ib32 = il / 8;
                const int half32 = (il / 4) & 1;
                const int row = il & 3;
                device const uchar *q3 = xb->qs + 8*ib32;
                device const ushort *gas =
                    (device const ushort *)(xb->qs + QK_K/4) + 2*ib32;
                const uint aux = gas[0] | (uint(gas[1]) << 16);
                const float dl = (float)xb->d *
                    (0.5f + float(aux >> 28)) * 0.5f;
                const uint sign_shift = 14*half32 + 7*(row/2);
                const uchar signs = sign_lut[(aux >> sign_shift) & 127u];
                const float4 vx = q36_dense_iq3_grid4_tg(
                    grid, q3[4*half32 + row], signs, bool(row & 1));
                sum[row_out] += dl * dot(vx, vy[part]);
            }
        }
    }
#pragma unroll(4)
    for (short row = 0; row < 4; ++row) {
        const float total = simd_sum(sum[row]);
        if (tiisg == 0 && first_row + row < args.ne01)
            ((device float *)dst)[first_row + row] = total;
    }
}

kernel void kernel_mul_mv_rows_iq3_s_tg_f32(
        constant q36_metal_args_mul_mv_ext & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3   tgpig[[threadgroup_position_in_grid]],
        ushort  tiisg[[thread_index_in_simdgroup]],
        ushort  sgitg[[simdgroup_index_in_threadgroup]]) {
    const short NSG = FC_mul_mv_nsg;
    const ushort lid = 32 * sgitg + tiisg;
    threadgroup uint *grid = (threadgroup uint *)shmem;
    for (ushort i = lid; i < 512; i += 32 * NSG)
        grid[i] = q36_dense_iq3s_grid[i];
    threadgroup_barrier(mem_flags::mem_threadgroup);

    const int first_row = (tgpig.x * NSG + sgitg) * 4;
    const int blocks = args.ne00 / 256;
    device const float4 * y4 = (device const float4 *)src1;
    float sum[4] = {0.0f};
    for (int block = 0; block < blocks; ++block) {
        const float4 vy[2] = {
            y4[block * 64 + tiisg], y4[block * 64 + 32 + tiisg]
        };
#pragma unroll(4)
        for (short row_out = 0; row_out < 4; ++row_out) {
            if (first_row + row_out >= args.ne01) continue;
            device const q36_dense_block_iq3_s *xb =
                (device const q36_dense_block_iq3_s *)(
                    src0 + (uint64_t)(first_row + row_out) * args.nb01) + block;
#pragma unroll(2)
            for (short part = 0; part < 2; ++part) {
                const short il = tiisg + 32 * part;
                const int ib32 = il / 8;
                const int half32 = (il / 4) & 1;
                const int row = il & 3;
                device const uchar *qs = xb->qs + 8*ib32;
                device const uchar *signs = xb->signs + 4*ib32 + 2*half32;
                const uchar qh = xb->qh[ib32] >> (4*half32);
                const uint grid_index = qs[4*half32 + row] |
                    ((uint(qh) << (8 - row)) & 256u);
                const float dl = (float)xb->d * float(1 + 2*((
                    xb->scales[ib32/2] >> (4*(ib32%2))) & 0xf));
                const float4 vx = q36_dense_iq3_grid4_tg(
                    grid, grid_index, signs[row/2], bool(row & 1));
                sum[row_out] += dl * dot(vx, vy[part]);
            }
        }
    }
#pragma unroll(4)
    for (short row = 0; row < 4; ++row) {
        const float total = simd_sum(sum[row]);
        if (tiisg == 0 && first_row + row < args.ne01)
            ((device float *)dst)[first_row + row] = total;
    }
}

kernel void kernel_mul_mv_rows_iq3_s_pair_tg_f32(
        constant q36_metal_args_mul_mv_ext &args,
        device const char *src0_a,
        device const char *src0_b,
        device const char *src1,
        device char *dst_a,
        device char *dst_b,
        threadgroup char *shmem [[threadgroup(0)]],
        uint3 tgpig [[threadgroup_position_in_grid]],
        ushort tiisg [[thread_index_in_simdgroup]],
        ushort sgitg [[simdgroup_index_in_threadgroup]]) {
    const short NSG = FC_mul_mv_nsg;
    const ushort lid = 32 * sgitg + tiisg;
    threadgroup uint *grid = (threadgroup uint *)shmem;
    for (ushort i = lid; i < 512; i += 32 * NSG)
        grid[i] = q36_dense_iq3s_grid[i];
    threadgroup_barrier(mem_flags::mem_threadgroup);

    const int first_row = (tgpig.x * NSG + sgitg) * 4;
    const int blocks = args.ne00 / 256;
    device const float4 *y4 = (device const float4 *)src1;
    float sum_a[4] = {0.0f};
    float sum_b[4] = {0.0f};
    for (int block = 0; block < blocks; ++block) {
        const float4 vy[2] = {
            y4[block * 64 + tiisg], y4[block * 64 + 32 + tiisg]
        };
#pragma unroll(4)
        for (short row_out = 0; row_out < 4; ++row_out) {
            if (first_row + row_out >= args.ne01) continue;
            device const q36_dense_block_iq3_s *xa =
                (device const q36_dense_block_iq3_s *)(
                    src0_a + (uint64_t)(first_row + row_out) * args.nb01) + block;
            device const q36_dense_block_iq3_s *xb =
                (device const q36_dense_block_iq3_s *)(
                    src0_b + (uint64_t)(first_row + row_out) * args.nb01) + block;
#pragma unroll(2)
            for (short part = 0; part < 2; ++part) {
                const short il = tiisg + 32 * part;
                const int ib32 = il / 8;
                const int half32 = (il / 4) & 1;
                const int row = il & 3;

                device const uchar *qsa = xa->qs + 8*ib32;
                device const uchar *signsa = xa->signs + 4*ib32 + 2*half32;
                const uchar qha = xa->qh[ib32] >> (4*half32);
                const uint grid_a = qsa[4*half32 + row] |
                    ((uint(qha) << (8 - row)) & 256u);
                const float dla = (float)xa->d * float(1 + 2*((
                    xa->scales[ib32/2] >> (4*(ib32%2))) & 0xf));
                const float4 va = q36_dense_iq3_grid4_tg(
                    grid, grid_a, signsa[row/2], bool(row & 1));

                device const uchar *qsb = xb->qs + 8*ib32;
                device const uchar *signsb = xb->signs + 4*ib32 + 2*half32;
                const uchar qhb = xb->qh[ib32] >> (4*half32);
                const uint grid_b = qsb[4*half32 + row] |
                    ((uint(qhb) << (8 - row)) & 256u);
                const float dlb = (float)xb->d * float(1 + 2*((
                    xb->scales[ib32/2] >> (4*(ib32%2))) & 0xf));
                const float4 vb = q36_dense_iq3_grid4_tg(
                    grid, grid_b, signsb[row/2], bool(row & 1));

                sum_a[row_out] += dla * dot(va, vy[part]);
                sum_b[row_out] += dlb * dot(vb, vy[part]);
            }
        }
    }
#pragma unroll(4)
    for (short row = 0; row < 4; ++row) {
        const float total_a = simd_sum(sum_a[row]);
        const float total_b = simd_sum(sum_b[row]);
        if (tiisg == 0 && first_row + row < args.ne01) {
            ((device float *)dst_a)[first_row + row] = total_a;
            ((device float *)dst_b)[first_row + row] = total_b;
        }
    }
}

kernel void kernel_mul_mv_rows_iq3_xxs_pair_tg_f32(
        constant q36_metal_args_mul_mv_ext &args,
        device const char *src0_a,
        device const char *src0_b,
        device const char *src1,
        device char *dst_a,
        device char *dst_b,
        threadgroup char *shmem [[threadgroup(0)]],
        uint3 tgpig [[threadgroup_position_in_grid]],
        ushort tiisg [[thread_index_in_simdgroup]],
        ushort sgitg [[simdgroup_index_in_threadgroup]]) {
    const short NSG = FC_mul_mv_nsg;
    const ushort lid = 32 * sgitg + tiisg;
    threadgroup uint *grid = (threadgroup uint *)shmem;
    threadgroup uchar *sign_lut = (threadgroup uchar *)(grid + 256);
    for (ushort i = lid; i < 256; i += 32 * NSG)
        grid[i] = q36_dense_iq3xxs_grid[i];
    for (ushort i = lid; i < 128; i += 32 * NSG)
        sign_lut[i] = q36_dense_ksigns_iq3[i];
    threadgroup_barrier(mem_flags::mem_threadgroup);

    const int first_row = (tgpig.x * NSG + sgitg) * 4;
    const int blocks = args.ne00 / 256;
    device const float4 *y4 = (device const float4 *)src1;
    float sum_a[4] = {0.0f};
    float sum_b[4] = {0.0f};
    for (int block = 0; block < blocks; ++block) {
        const float4 vy[2] = {
            y4[block * 64 + tiisg], y4[block * 64 + 32 + tiisg]
        };
#pragma unroll(4)
        for (short row_out = 0; row_out < 4; ++row_out) {
            if (first_row + row_out >= args.ne01) continue;
            device const q36_dense_block_iq3_xxs *xa =
                (device const q36_dense_block_iq3_xxs *)(
                    src0_a + (uint64_t)(first_row + row_out) * args.nb01) + block;
            device const q36_dense_block_iq3_xxs *xb =
                (device const q36_dense_block_iq3_xxs *)(
                    src0_b + (uint64_t)(first_row + row_out) * args.nb01) + block;
#pragma unroll(2)
            for (short part = 0; part < 2; ++part) {
                const short il = tiisg + 32 * part;
                const int ib32 = il / 8;
                const int half32 = (il / 4) & 1;
                const int row = il & 3;

                device const uchar *q3a = xa->qs + 8*ib32;
                device const ushort *gasa =
                    (device const ushort *)(xa->qs + QK_K/4) + 2*ib32;
                const uint auxa = gasa[0] | (uint(gasa[1]) << 16);
                const float dla = (float)xa->d *
                    (0.5f + float(auxa >> 28)) * 0.5f;
                const uint shifta = 14*half32 + 7*(row/2);
                const uchar signsa = sign_lut[(auxa >> shifta) & 127u];
                const float4 va = q36_dense_iq3_grid4_tg(
                    grid, q3a[4*half32 + row], signsa, bool(row & 1));

                device const uchar *q3b = xb->qs + 8*ib32;
                device const ushort *gasb =
                    (device const ushort *)(xb->qs + QK_K/4) + 2*ib32;
                const uint auxb = gasb[0] | (uint(gasb[1]) << 16);
                const float dlb = (float)xb->d *
                    (0.5f + float(auxb >> 28)) * 0.5f;
                const uint shiftb = 14*half32 + 7*(row/2);
                const uchar signsb = sign_lut[(auxb >> shiftb) & 127u];
                const float4 vb = q36_dense_iq3_grid4_tg(
                    grid, q3b[4*half32 + row], signsb, bool(row & 1));

                sum_a[row_out] += dla * dot(va, vy[part]);
                sum_b[row_out] += dlb * dot(vb, vy[part]);
            }
        }
    }
#pragma unroll(4)
    for (short row = 0; row < 4; ++row) {
        const float total_a = simd_sum(sum_a[row]);
        const float total_b = simd_sum(sum_b[row]);
        if (tiisg == 0 && first_row + row < args.ne01) {
            ((device float *)dst_a)[first_row + row] = total_a;
            ((device float *)dst_b)[first_row + row] = total_b;
        }
    }
}

typedef decltype(kernel_mul_mv_ext_q4_f32_disp<2, block_q8_0, 32, dequantize_q8_0_t4>) mul_mv_ext_q4_f32_t;
typedef decltype(kernel_mul_mv_rows_q4_f32_disp<2, q36_dense_block_q4_K, 256, dequantize_dense_q4_K_t4>) mul_mv_rows_q_f32_t;

// Host-visible small-batch variants for r1=2..5 during tiny prompt/support
// paths.

template<short r1ptg>
void kernel_mul_mv_ext_q8_0_pair_swiglu_f32_impl(
        constant q36_metal_args_mul_mv_ext & args,
        device const char * src0_gate,
        device const char * src0_up,
        device const char * src1,
        device       char * dst_gate,
        device       char * dst_up,
        device       char * dst_mid,
        constant     float &clamp_value,
        uint3   tgpig[[threadgroup_position_in_grid]],
        ushort  tiisg[[thread_index_in_simdgroup]],
        ushort  sgitg[[simdgroup_index_in_threadgroup]]) {
    const short NSG   = FC_mul_mv_nsg;
    const short nxpsg = FC_mul_mv_nxpsg;

    const short chpt = 4;
    const short chpb = 8;
    const short nypsg = (32/nxpsg);

    const short tx = tiisg%nxpsg;
    const short ty = tiisg/nxpsg;

    const int i01 = tgpig.x*(nypsg*NSG) + nypsg*sgitg + ty;
    const int i11 = tgpig.y*r1ptg;
    const int i1m = tgpig.z;

    const int i12 = i1m%args.ne12;
    const int i13 = i1m/args.ne12;

    const uint64_t offset0 = i01*args.nb01 + (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;
    const uint64_t offset1 = i11*args.nb11 + (i12        )*args.nb12 + (i13        )*args.nb13;

    device const block_q8_0 * xq_gate =
        (i01 < args.ne01) ? (device const block_q8_0 *)(src0_gate + offset0) + tx/chpb
                          : (device const block_q8_0 *)src0_gate;
    device const block_q8_0 * xq_up =
        (i01 < args.ne01) ? (device const block_q8_0 *)(src0_up + offset0) + tx/chpb
                          : (device const block_q8_0 *)src0_up;

    device const float4 * y4[r1ptg];
    for (int ir1 = 0; ir1 < r1ptg; ++ir1) {
        y4[ir1] = (i11 + ir1 < args.ne11)
            ? (device const float4 *)(src1 + offset1 + ir1*args.nb11) + tx
            : (device const float4 *)src1;
    }

    float sum_gate[r1ptg] = { [ 0 ... r1ptg - 1 ] = 0.0f };
    float sum_up[r1ptg]   = { [ 0 ... r1ptg - 1 ] = 0.0f };

    short cch = tx%chpb;
    for (int ich = tx; 4*ich < args.ne00; ich += chpt*nxpsg) {
        float4 lg[chpt];
        float4 lu[chpt];

#pragma unroll(chpt)
        for (short ch = 0; ch < chpt; ++ch) {
            dequantize_q8_0_t4(xq_gate, cch, lg[ch]);
            dequantize_q8_0_t4(xq_up,   cch, lu[ch]);

            cch += nxpsg;
            if (cch >= chpb) {
                xq_gate += cch/chpb;
                xq_up   += cch/chpb;
                cch %= chpb;
            }
        }

#pragma unroll(chpt)
        for (short ch = 0; ch < chpt; ++ch) {
#pragma unroll(r1ptg)
            for (short ir1 = 0; ir1 < r1ptg; ++ir1) {
                const float4 y = y4[ir1][ch*nxpsg];
                sum_gate[ir1] += dot(lg[ch], y);
                sum_up[ir1] += dot(lu[ch], y);
            }
        }

#pragma unroll(r1ptg)
        for (short ir1 = 0; ir1 < r1ptg; ++ir1) {
            y4[ir1] += chpt*nxpsg;
        }
    }

    for (short ir1 = 0; ir1 < r1ptg; ++ir1) {
        if (nxpsg >= 32) {
            sum_gate[ir1] += simd_shuffle_down(sum_gate[ir1], 16);
            sum_up[ir1]   += simd_shuffle_down(sum_up[ir1],   16);
        }
        if (nxpsg >= 16) {
            sum_gate[ir1] += simd_shuffle_down(sum_gate[ir1],  8);
            sum_up[ir1]   += simd_shuffle_down(sum_up[ir1],    8);
        }
        if (nxpsg >= 8) {
            sum_gate[ir1] += simd_shuffle_down(sum_gate[ir1],  4);
            sum_up[ir1]   += simd_shuffle_down(sum_up[ir1],    4);
        }
        if (nxpsg >= 4) {
            sum_gate[ir1] += simd_shuffle_down(sum_gate[ir1],  2);
            sum_up[ir1]   += simd_shuffle_down(sum_up[ir1],    2);
        }
        if (nxpsg >= 2) {
            sum_gate[ir1] += simd_shuffle_down(sum_gate[ir1],  1);
            sum_up[ir1]   += simd_shuffle_down(sum_up[ir1],    1);
        }
    }

    if (tx == 0 && i01 < args.ne01) {
        for (short ir1 = 0; ir1 < r1ptg && i11 + ir1 < args.ne11; ++ir1) {
            const uint64_t dst_base =
                (uint64_t)i1m*args.ne0*args.ne1 + (uint64_t)(i11 + ir1)*args.ne0;
            device float * gate_f32 = (device float *)dst_gate + dst_base;
            device float * up_f32   = (device float *)dst_up   + dst_base;
            device float * mid_f32  = (device float *)dst_mid  + dst_base;

            const float gate = sum_gate[ir1];
            const float up = sum_up[ir1];
            gate_f32[i01] = gate;
            up_f32[i01] = up;

            float g = gate;
            float u = up;
            if (clamp_value > 1.0e-6f) {
                g = min(g, clamp_value);
                u = clamp(u, -clamp_value, clamp_value);
            }
            const float silu = g / (1.0f + exp(-g));
            mid_f32[i01] = silu * u;
        }
    }
}

template<short r1ptg>
kernel void kernel_mul_mv_ext_q8_0_pair_swiglu_f32_disp(
        constant q36_metal_args_mul_mv_ext & args,
        device const char * src0_gate,
        device const char * src0_up,
        device const char * src1,
        device       char * dst_gate,
        device       char * dst_up,
        device       char * dst_mid,
        constant     float &clamp_value,
        uint3   tgpig[[threadgroup_position_in_grid]],
        ushort  tiisg[[thread_index_in_simdgroup]],
        ushort  sgitg[[simdgroup_index_in_threadgroup]]) {
    kernel_mul_mv_ext_q8_0_pair_swiglu_f32_impl<r1ptg>(
            args, src0_gate, src0_up, src1, dst_gate, dst_up, dst_mid, clamp_value,
            tgpig, tiisg, sgitg);
}

typedef decltype(kernel_mul_mv_ext_q8_0_pair_swiglu_f32_disp<2>) mul_mv_ext_q8_0_pair_swiglu_f32_t;

// Host-visible small-batch variants. Q36 currently needs F16 and Q8_0 weights
// for r1=2..5 during the prompt path.

template [[host_name("kernel_mul_mv_ext_q8_0_f32_r1_2")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<2, block_q8_0, 32, dequantize_q8_0_t4>;
template [[host_name("kernel_mul_mv_ext_q8_0_f32_r1_3")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<3, block_q8_0, 32, dequantize_q8_0_t4>;
template [[host_name("kernel_mul_mv_ext_q8_0_f32_r1_4")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<4, block_q8_0, 32, dequantize_q8_0_t4>;
template [[host_name("kernel_mul_mv_ext_q8_0_f32_r1_5")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<5, block_q8_0, 32, dequantize_q8_0_t4>;

template [[host_name("kernel_mul_mv_ext_q4_0_f32_r1_1")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<1, q36_dense_block_q4_0, 32,  dequantize_dense_q4_0_t4>;
template [[host_name("kernel_mul_mv_ext_q4_0_f32_r1_2")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<2, q36_dense_block_q4_0, 32,  dequantize_dense_q4_0_t4>;
template [[host_name("kernel_mul_mv_ext_q4_0_f32_r1_3")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<3, q36_dense_block_q4_0, 32,  dequantize_dense_q4_0_t4>;
template [[host_name("kernel_mul_mv_ext_q4_0_f32_r1_4")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<4, q36_dense_block_q4_0, 32,  dequantize_dense_q4_0_t4>;
template [[host_name("kernel_mul_mv_ext_q4_0_f32_r1_5")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<5, q36_dense_block_q4_0, 32,  dequantize_dense_q4_0_t4>;

template [[host_name("kernel_mul_mv_ext_q4_K_f32_r1_1")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<1, q36_dense_block_q4_K, 256, dequantize_dense_q4_K_t4>;
template [[host_name("kernel_mul_mv_rows_q4_K_f32")]] kernel mul_mv_rows_q_f32_t kernel_mul_mv_rows_q4_f32_disp<2, q36_dense_block_q4_K, 256, dequantize_dense_q4_K_t4>;
template [[host_name("kernel_mul_mv_ext_q4_K_f32_r1_2")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<2, q36_dense_block_q4_K, 256, dequantize_dense_q4_K_t4>;
template [[host_name("kernel_mul_mv_ext_q4_K_f32_r1_3")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<3, q36_dense_block_q4_K, 256, dequantize_dense_q4_K_t4>;
template [[host_name("kernel_mul_mv_ext_q4_K_f32_r1_4")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<4, q36_dense_block_q4_K, 256, dequantize_dense_q4_K_t4>;
template [[host_name("kernel_mul_mv_ext_q4_K_f32_r1_5")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<5, q36_dense_block_q4_K, 256, dequantize_dense_q4_K_t4>;

template [[host_name("kernel_mul_mv_ext_q6_K_f32_r1_1")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<1, q36_dense_block_q6_K, 256, dequantize_dense_256_t4<q36_dense_block_q6_K, dequantize_dense_q6_K>>;
template [[host_name("kernel_mul_mv_rows_q6_K_f32")]] kernel mul_mv_rows_q_f32_t kernel_mul_mv_rows_q4_f32_disp<2, q36_dense_block_q6_K, 256, dequantize_dense_256_t4<q36_dense_block_q6_K, dequantize_dense_q6_K>>;
template [[host_name("kernel_mul_mv_ext_q6_K_f32_r1_2")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<2, q36_dense_block_q6_K, 256, dequantize_dense_256_t4<q36_dense_block_q6_K, dequantize_dense_q6_K>>;
template [[host_name("kernel_mul_mv_ext_q6_K_f32_r1_3")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<3, q36_dense_block_q6_K, 256, dequantize_dense_256_t4<q36_dense_block_q6_K, dequantize_dense_q6_K>>;
template [[host_name("kernel_mul_mv_ext_q6_K_f32_r1_4")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<4, q36_dense_block_q6_K, 256, dequantize_dense_256_t4<q36_dense_block_q6_K, dequantize_dense_q6_K>>;
template [[host_name("kernel_mul_mv_ext_q6_K_f32_r1_5")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<5, q36_dense_block_q6_K, 256, dequantize_dense_256_t4<q36_dense_block_q6_K, dequantize_dense_q6_K>>;

#define Q36_DENSE_MV_VARIANTS(stem, block_t, deq, rows) \
template [[host_name("kernel_mul_mv_ext_" #stem "_f32_r1_1")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<1, block_t, 256, deq>; \
template [[host_name("kernel_mul_mv_ext_" #stem "_f32_r1_2")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<2, block_t, 256, deq>; \
template [[host_name("kernel_mul_mv_ext_" #stem "_f32_r1_3")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<3, block_t, 256, deq>; \
template [[host_name("kernel_mul_mv_ext_" #stem "_f32_r1_4")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<4, block_t, 256, deq>; \
template [[host_name("kernel_mul_mv_ext_" #stem "_f32_r1_5")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<5, block_t, 256, deq>; \
template [[host_name("kernel_mul_mv_rows_" #stem "_f32")]] kernel mul_mv_rows_q_f32_t kernel_mul_mv_rows_q4_f32_disp<rows, block_t, 256, deq>;

Q36_DENSE_MV_VARIANTS(q2_K, q36_dense_block_q2_K,
                      dequantize_dense_q2_K_t4, 4)
Q36_DENSE_MV_VARIANTS(q3_K, q36_dense_block_q3_K,
                      dequantize_dense_q3_K_t4, 4)
Q36_DENSE_MV_VARIANTS(q5_K, q36_dense_block_q5_K,
                      dequantize_dense_q5_K_t4, 4)
Q36_DENSE_MV_VARIANTS(iq2_xxs, q36_dense_block_iq2_xxs,
                      dequantize_dense_iq2_xxs_t4, 4)
Q36_DENSE_MV_VARIANTS(iq2_xs, q36_dense_block_iq2_xs,
                      dequantize_dense_iq2_xs_t4, 4)
Q36_DENSE_MV_VARIANTS(iq2_s, q36_dense_block_iq2_s,
                      dequantize_dense_iq2_s_t4, 4)
Q36_DENSE_MV_VARIANTS(iq1_s, q36_dense_block_iq1_s,
                      dequantize_dense_iq1_s_t4, 4)
Q36_DENSE_MV_VARIANTS(iq4_nl, q36_dense_block_iq4_nl,
                      dequantize_dense_iq4_nl_t4, 4)
Q36_DENSE_MV_VARIANTS(iq4_xs, q36_dense_block_iq4_xs,
                      dequantize_dense_iq4_xs_t4, 4)

#undef Q36_DENSE_MV_VARIANTS

kernel void kernel_mul_mv_rows_q5_K_expanded_f32(
        constant q36_metal_args_mul_mv_ext &args,
        device const uchar *src0,
        device const float *src1,
        device float *dst,
        uint3 tgpig [[threadgroup_position_in_grid]],
        ushort tiisg [[thread_index_in_simdgroup]],
        ushort sgitg [[simdgroup_index_in_threadgroup]]) {
    const int row = tgpig.x * 4 + sgitg;
    if (row >= args.ne01) return;
    const int blocks_per_row = args.ne00 / 256;
    float sum = 0.0f;
    for (int block = 0; block < blocks_per_row; ++block) {
        const uint64_t block_index =
            (uint64_t)row * (uint64_t)blocks_per_row + (uint64_t)block;
        device const q36_dense_block_q5_K_expanded *xb =
            (device const q36_dense_block_q5_K_expanded *)(
                src0 + block_index * 276u);
        device const uchar *raw = (device const uchar *)xb;
        for (uint half_id = 0; half_id < 2u; ++half_id) {
            const uint within = 4u * uint(tiisg) + 128u * half_id;
            const uint group = within >> 5u;
            const float dl = float(*((device const half *)(raw + 0u))) *
                             float(raw[4u + group]);
            const float ml = float(*((device const half *)(raw + 2u))) *
                             float(raw[12u + group]);
            const uint k = uint(block) * 256u + within;
            for (uint i = 0; i < 4u; ++i)
                sum += (dl * float(raw[20u + within + i]) - ml) *
                       src1[k + i];
        }
    }
    const float total = simd_sum(sum);
    if (tiisg == 0) dst[row] = total;
}

template [[host_name("kernel_mul_mv_ext_iq3_xxs_f32_r1_1")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<1, q36_dense_block_iq3_xxs, 256, dequantize_dense_iq3_xxs_t4>;
template [[host_name("kernel_mul_mv_ext_iq3_xxs_f32_r1_2")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<2, q36_dense_block_iq3_xxs, 256, dequantize_dense_iq3_xxs_t4>;
template [[host_name("kernel_mul_mv_ext_iq3_xxs_f32_r1_3")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<3, q36_dense_block_iq3_xxs, 256, dequantize_dense_iq3_xxs_t4>;
template [[host_name("kernel_mul_mv_ext_iq3_xxs_f32_r1_4")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<4, q36_dense_block_iq3_xxs, 256, dequantize_dense_iq3_xxs_t4>;
template [[host_name("kernel_mul_mv_ext_iq3_xxs_f32_r1_5")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<5, q36_dense_block_iq3_xxs, 256, dequantize_dense_iq3_xxs_t4>;

template [[host_name("kernel_mul_mv_ext_iq3_s_f32_r1_1")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<1, q36_dense_block_iq3_s, 256, dequantize_dense_iq3_s_t4>;
template [[host_name("kernel_mul_mv_ext_iq3_s_f32_r1_2")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<2, q36_dense_block_iq3_s, 256, dequantize_dense_iq3_s_t4>;
template [[host_name("kernel_mul_mv_ext_iq3_s_f32_r1_3")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<3, q36_dense_block_iq3_s, 256, dequantize_dense_iq3_s_t4>;
template [[host_name("kernel_mul_mv_ext_iq3_s_f32_r1_4")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<4, q36_dense_block_iq3_s, 256, dequantize_dense_iq3_s_t4>;
template [[host_name("kernel_mul_mv_ext_iq3_s_f32_r1_5")]] kernel mul_mv_ext_q4_f32_t kernel_mul_mv_ext_q4_f32_disp<5, q36_dense_block_iq3_s, 256, dequantize_dense_iq3_s_t4>;

template [[host_name("kernel_mul_mv_ext_q8_0_pair_swiglu_f32_r1_2")]] kernel mul_mv_ext_q8_0_pair_swiglu_f32_t kernel_mul_mv_ext_q8_0_pair_swiglu_f32_disp<2>;
template [[host_name("kernel_mul_mv_ext_q8_0_pair_swiglu_f32_r1_3")]] kernel mul_mv_ext_q8_0_pair_swiglu_f32_t kernel_mul_mv_ext_q8_0_pair_swiglu_f32_disp<3>;
template [[host_name("kernel_mul_mv_ext_q8_0_pair_swiglu_f32_r1_4")]] kernel mul_mv_ext_q8_0_pair_swiglu_f32_t kernel_mul_mv_ext_q8_0_pair_swiglu_f32_disp<4>;
template [[host_name("kernel_mul_mv_ext_q8_0_pair_swiglu_f32_r1_5")]] kernel mul_mv_ext_q8_0_pair_swiglu_f32_t kernel_mul_mv_ext_q8_0_pair_swiglu_f32_disp<5>;

constant bool FC_mul_mm_bc_inp [[function_constant(FC_MUL_MM + 0)]];
constant bool FC_mul_mm_bc_out [[function_constant(FC_MUL_MM + 1)]];

#ifdef Q36_METAL_HAS_TENSOR
template<
    short NR0, short NR1,
    typename SA, typename SA_4x4, typename block_q, short nl,
    void (*dequantize_func)(device const block_q *, short, thread SA_4x4 &),
    typename T0, typename T0_4x4, typename T1>
kernel void kernel_mul_mm_mpp(
        constant q36_metal_args_mul_mm & args,
        device const char * srcA,
        device const char * srcB,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig [[threadgroup_position_in_grid]],
        ushort tiitg [[thread_index_in_threadgroup]],
        ushort sgitg [[simdgroup_index_in_threadgroup]]) {
    (void) sgitg;

    constexpr int NK  = 32;
    constexpr int NL  = NK/16;
    constexpr int NUM_THREADS = 128;

    const int K = args.ne00;
    const int M = args.ne0;
    const int N = args.ne1;
    const int im = tgpig.z;
    const int i12 = im%args.ne12;
    const int i13 = im/args.ne12;
    const int r0 = tgpig.y*NR0;
    const int r1 = tgpig.x*NR1;

    const uint64_t offset0 = (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;

    threadgroup SA *sa = (threadgroup SA *)shmem;
    threadgroup SA *sb = sa + NR0*NK;
    auto tA = tensor(sa, dextents<int32_t, 2>(NK, NR0));
    auto tB = tensor(sb, dextents<int32_t, 2>(NK, NR1));

    device const T1 *ptrB = (device const T1 *)(srcB + args.nb12*i12 + args.nb13*i13);
    const int strideB = args.nb11/sizeof(T1);

    matmul2d<
        matmul2d_descriptor(NR1, NR0, NK, false, true, false,
            matmul2d_descriptor::mode::multiply_accumulate),
        execution_simdgroups<4>> mm;

    auto cT = mm.template get_destination_cooperative_tensor<decltype(tB), decltype(tA), float>();

    #pragma unroll
    for (uint16_t i = 0; i < cT.get_capacity(); ++i) {
        if (cT.is_valid_element(i)) {
            cT[i] = 0.0f;
        }
    }

    for (int loop_k = 0; loop_k < K; loop_k += NK) {
        for (int work = tiitg; work < NR0*NL; work += NUM_THREADS) {
            const int row = work/NL;
            const int k_chunk = work%NL;
            const int k_pos = loop_k + k_chunk*16;
            const short k_base = k_chunk*16;

            if (!FC_mul_mm_bc_out || r0 + row < M) {
                if (is_same<T0_4x4, block_q>::value && FC_mul_mm_bc_inp) {
                    device const T0 *row_ptr = (device const T0 *)(srcA + args.nb01*(r0 + row) + offset0);
                    FOR_UNROLL (short i = 0; i < 16; i++) {
                        sa[row*NK + k_base + i] = (k_pos + i < K) ? (SA)row_ptr[k_pos + i] : (SA)0;
                    }
                } else {
                    const int block_idx = k_pos/(16*nl);
                    const short il = (k_pos/16)%nl;
                    device const block_q *row_ptr = (device const block_q *)(srcA + args.nb01*(r0 + row) + offset0);

                    SA_4x4 temp_a;
                    dequantize_func(row_ptr + block_idx, il, temp_a);
                    FOR_UNROLL (short i = 0; i < 16; i++) {
                        sa[row*NK + k_base + i] = (k_pos + i < K) ? temp_a[i/4][i%4] : (SA)0;
                    }
                }
            } else {
                FOR_UNROLL (short i = 0; i < 16; i++) {
                    sa[row*NK + k_base + i] = (SA)0;
                }
            }
        }
        for (int work = tiitg; work < NK*NR1; work += NUM_THREADS) {
            const int col = work/NK;
            const int k = work%NK;
            if ((!FC_mul_mm_bc_out && !FC_mul_mm_bc_inp) ||
                (r1 + col < N && loop_k + k < K)) {
                sb[col*NK + k] = (SA)ptrB[(uint64_t)(r1 + col)*strideB + loop_k + k];
            } else {
                sb[col*NK + k] = (SA)0;
            }
        }

        threadgroup_barrier(mem_flags::mem_threadgroup);

        auto mA = tA.slice(0, 0);
        auto mB = tB.slice(0, 0);
        mm.run(mB, mA, cT);

        threadgroup_barrier(mem_flags::mem_threadgroup);
    }

    device float *dst_batch = (device float *)dst + im*N*M;
    if (!FC_mul_mm_bc_out) {
        device float *dst_tile = dst_batch + r0 + (uint64_t)r1*M;
        auto tD = tensor(dst_tile, dextents<int32_t, 2>(NR0, NR1), array<int, 2>({1, M}));
        cT.store(tD);
    } else {
        auto tD = tensor(dst_batch, dextents<int32_t, 2>(M, N), array<int, 2>({1, M}));
        auto mD = tD.slice(r0, r1);
        cT.store(mD);
    }
}

typedef decltype(kernel_mul_mm_mpp<64, 32, half, half4x4, float4x4, 1, dequantize_f32, float, float4x4, float>) mul_mm_mpp_t;

template [[host_name("kernel_mul_mm_f16_f32_mpp")]]  kernel mul_mm_mpp_t kernel_mul_mm_mpp<64, 32, half, half4x4, half4x4, 1, dequantize_f16,  half,  half4x4,  float>;

// Retained Metal4/TensorOps dense prefill kernel.  The legacy MPP prototype
// staged both operands in threadgroup memory; this version stages only the
// model weight tile and lets MPP read the dense RHS activation matrix directly
// from device memory.  That direct-RHS shape was the clear win for Q36's large
// aligned F16/Q8_0 prompt matmuls.  The host selects the widest token tile that
// evenly divides the batch, with 128-token tiles retained after the 64-token
// retest was neutral or slower.
//
// The host dispatch guarantees M % NR0 == 0 and K % NK == 0, so the dequant
// stage does no bounds work.  The weight tile is double-buffered: the next
// k-step's dequant overlaps the current cooperative matmul, so the k-loop
// needs one threadgroup barrier per step instead of two.
template<
    short NR1,
    typename SA, typename SA_4x4, typename block_q, short nl,
    void (*dequantize_func)(device const block_q *, short, thread SA_4x4 &),
    typename T0, typename T0_4x4, typename T1>
kernel void kernel_mul_mm_mpp_direct_rhs(
        constant q36_metal_args_mul_mm & args,
        device const char * srcA,
        device const char * srcB,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig [[threadgroup_position_in_grid]],
        ushort tiitg [[thread_index_in_threadgroup]],
        ushort sgitg [[simdgroup_index_in_threadgroup]]) {
    (void) sgitg;

    constexpr int NR0 = 64;
    constexpr int NK  = 32;
    constexpr int NL  = NK/16;
    constexpr int NUM_THREADS = 128;

    const int K = args.ne00;
    const int M = args.ne0;
    const int N = args.ne1;
    const int im = tgpig.z;
    const int i12 = im%args.ne12;
    const int i13 = im/args.ne12;
    const int r0 = tgpig.y*NR0;
    const int r1 = tgpig.x*NR1;

    const uint64_t offset0 = (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;

    threadgroup SA *sa = (threadgroup SA *)shmem;
    auto tA0 = tensor(sa,          dextents<int32_t, 2>(NK, NR0));
    auto tA1 = tensor(sa + NR0*NK, dextents<int32_t, 2>(NK, NR0));

    device T1 *ptrB = (device T1 *)(srcB + args.nb12*i12 + args.nb13*i13);
    const int strideB = args.nb11/sizeof(T1);
    auto tB = tensor(ptrB, dextents<int32_t, 2>(K, N), array<int, 2>({1, strideB}));

    matmul2d<
        matmul2d_descriptor(NR1, NR0, NK, false, true, true,
            matmul2d_descriptor::mode::multiply_accumulate),
        execution_simdgroups<4>> mm;

    auto cT = mm.template get_destination_cooperative_tensor<decltype(tB), decltype(tA0), float>();

    #pragma unroll
    for (uint16_t i = 0; i < cT.get_capacity(); ++i) {
        if (cT.is_valid_element(i)) {
            cT[i] = 0.0f;
        }
    }

    // NR0*NL/NUM_THREADS 16-value weight chunks per thread (1 at NR0=64).
    auto stage_tile = [&](const int loop_k, threadgroup SA *buf) {
        FOR_UNROLL (int work = tiitg; work < NR0*NL; work += NUM_THREADS) {
            const int row = work / NL;
            const int k_chunk = work % NL;
            const int k_pos = loop_k + k_chunk*16;
            const short k_base = k_chunk*16;
            if (is_same<T0_4x4, block_q>::value && FC_mul_mm_bc_inp) {
                device const T0 *row_ptr_f =
                    (device const T0 *)(srcA + args.nb01*(r0 + row) + offset0);
                FOR_UNROLL (short i = 0; i < 16; i++) {
                    buf[row*NK + k_base + i] = (SA)row_ptr_f[k_pos + i];
                }
            } else {
                device const block_q *row_ptr =
                    (device const block_q *)(srcA + args.nb01*(r0 + row) + offset0);
                SA_4x4 temp_a;
                dequantize_func(row_ptr + k_pos/(16*nl), (k_pos/16)%nl, temp_a);
                typedef vec<SA, 4> SA4;
                threadgroup SA4 *dst4 = (threadgroup SA4 *)(buf + row*NK + k_base);
                dst4[0] = temp_a[0];
                dst4[1] = temp_a[1];
                dst4[2] = temp_a[2];
                dst4[3] = temp_a[3];
            }
        }
    };

    stage_tile(0, sa);
    threadgroup_barrier(mem_flags::mem_threadgroup);

    uint buf_sel = 0;
    for (int loop_k = 0; loop_k < K; loop_k += NK) {
        auto mA = (buf_sel ? tA1 : tA0).slice(0, 0);
        auto mB = tB.slice(loop_k, r1);
        mm.run(mB, mA, cT);

        const int next_k = loop_k + NK;
        if (next_k < K) {
            buf_sel ^= 1u;
            stage_tile(next_k, buf_sel ? sa + NR0*NK : sa);
        }
        threadgroup_barrier(mem_flags::mem_threadgroup);
    }

    device float *dst_batch = (device float *)dst + im*N*M;
    auto tD = tensor(dst_batch, dextents<int32_t, 2>(M, N), array<int, 2>({1, M}));
    auto mD = tD.slice(r0, r1);
    cT.store(mD);
}

typedef decltype(kernel_mul_mm_mpp_direct_rhs<32, half, half4x4, float4x4, 1, dequantize_f32, float, float4x4, float>) mul_mm_mpp_direct_rhs_t;

template [[host_name("kernel_mul_mm_f16_f32_mpp_direct_rhs")]]  kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<32, half, half4x4, half4x4, 1, dequantize_f16,  half,  half4x4,  float>;
template [[host_name("kernel_mul_mm_f16_f32_mpp_direct_rhs_n64")]]  kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<64, half, half4x4, half4x4, 1, dequantize_f16,  half,  half4x4,  float>;
template [[host_name("kernel_mul_mm_f16_f32_mpp_direct_rhs_n128")]]  kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<128, half, half4x4, half4x4, 1, dequantize_f16,  half,  half4x4,  float>;
template [[host_name("kernel_mul_mm_q4_0_f32_nax_direct_rhs")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<32, half, half4x4, q36_dense_block_q4_0, 2, dequantize_dense_q4_0, float, float4x4, float>;
template [[host_name("kernel_mul_mm_q4_0_f32_nax_direct_rhs_n64")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<64, half, half4x4, q36_dense_block_q4_0, 2, dequantize_dense_q4_0, float, float4x4, float>;
template [[host_name("kernel_mul_mm_q4_0_f32_nax_direct_rhs_n128")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<128, half, half4x4, q36_dense_block_q4_0, 2, dequantize_dense_q4_0, float, float4x4, float>;
template [[host_name("kernel_mul_mm_q4_K_f32_nax_direct_rhs")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<32, half, half4x4, q36_dense_block_q4_K, 16, dequantize_dense_q4_K, float, float4x4, float>;
template [[host_name("kernel_mul_mm_q4_K_f32_nax_direct_rhs_n64")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<64, half, half4x4, q36_dense_block_q4_K, 16, dequantize_dense_q4_K, float, float4x4, float>;
template [[host_name("kernel_mul_mm_q4_K_f32_nax_direct_rhs_n128")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<128, half, half4x4, q36_dense_block_q4_K, 16, dequantize_dense_q4_K, float, float4x4, float>;
template [[host_name("kernel_mul_mm_q8_0_f32_nax_direct_rhs")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<32, half, half4x4, block_q8_0, 2, dequantize_q8_0_pairs, float, float4x4, float>;
template [[host_name("kernel_mul_mm_q8_0_f32_nax_direct_rhs_n64")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<64, half, half4x4, block_q8_0, 2, dequantize_q8_0_pairs, float, float4x4, float>;
template [[host_name("kernel_mul_mm_q8_0_f32_nax_direct_rhs_n128")]] kernel mul_mm_mpp_direct_rhs_t kernel_mul_mm_mpp_direct_rhs<128, half, half4x4, block_q8_0, 2, dequantize_q8_0_pairs, float, float4x4, float>;

#endif

// Tiled matrix-matrix kernel used for prompt batches larger than 8. Q36 uses
// this to turn prefill into large simdgroup matrix operations; each block_q
// contains 16*nl weights.
template<typename S0, typename S0_4x4, typename S0_8x8, typename S1, typename S1_2x4, typename S1_8x8, typename block_q, short nl, void (*dequantize_func)(device const block_q *, short, thread S0_4x4 &), typename T0, typename T0_4x4, typename T1, typename T1_2x4>
kernel void kernel_mul_mm(
        constant q36_metal_args_mul_mm & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiitg[[thread_index_in_threadgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {

    threadgroup S0 * sa = (threadgroup S0 *)(shmem);
    threadgroup S1 * sb =
        (threadgroup S1 *)(shmem + sizeof(S0) * 64 * 32);

    constexpr int NR0 = 64;
    constexpr int NR1 = 32;
    constexpr int NK  = 32;
    constexpr int NL0 = NK/16;
    constexpr int NL1 = NK/8;

    const int im = tgpig.z;
    const int r0 = tgpig.y*NR0;
    const int r1 = tgpig.x*NR1;

    // if this block is of 64x32 shape or smaller
    const short nr0 = (args.ne0 - r0 < NR0) ? (args.ne0 - r0) : NR0;
    const short nr1 = (args.ne1 - r1 < NR1) ? (args.ne1 - r1) : NR1;

    // a thread shouldn't load data outside of the matrix
    const short lr0 = ((short)tiitg/NL0) < nr0 ? ((short)tiitg/NL0) : nr0 - 1; // 0 .. 63
    const short lr1 = ((short)tiitg/NL1) < nr1 ? ((short)tiitg/NL1) : nr1 - 1; // 0 .. 31
    const short il0 = (tiitg % NL0);

    short il = il0;

    const int i12 = im%args.ne12;
    const int i13 = im/args.ne12;

    const uint64_t offset0 = (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;
    const short    offset1 = il0/nl;

    device const block_q * x = (device const block_q *)(src0 + args.nb01*(r0 + lr0) + offset0) + offset1;

    const short iy = 8*(tiitg % NL1);

    device const T1 * y = (device const T1 *)(src1
        + args.nb13*i13
        + args.nb12*i12
        + args.nb11*(r1 + lr1)
        + args.nb10*iy);

    S0_8x8 ma[4];
    S1_8x8 mb[2];

    simdgroup_float8x8 mc[8];

    for (short i = 0; i < 8; i++){
        mc[i] = make_filled_simdgroup_matrix<float, 8>(0.f);
    }

    for (int loop_k = 0; loop_k < args.ne00; loop_k += NK) {
        // load data and store to threadgroup memory
        if (is_same<T0_4x4, block_q>::value && FC_mul_mm_bc_inp) {
            threadgroup_barrier(mem_flags::mem_threadgroup);

            // no need for dequantization
            for (short i = 0; i < 16; i++) {
                const short sx = 2*il0 + i/8;
                const short sy = (tiitg/NL0)/8;

                const short lx = (tiitg/NL0)%8;
                const short ly = i%8;
                const short ib = 8*sx + sy;
                *(sa + 64*ib + 8*ly + lx) = loop_k + 16*il + i < args.ne00 ? *((device T0 *) x + i) : 0;
            }
        } else {
            S0_4x4 temp_a;
            dequantize_func(x, il, temp_a);

            threadgroup_barrier(mem_flags::mem_threadgroup);

            FOR_UNROLL (short i = 0; i < 16; i++) {
                const short sx = 2*il0 + i/8;
                const short sy = (tiitg/NL0)/8;

                const short lx = (tiitg/NL0)%8;
                const short ly = i%8;
                const short ib = 8*sx + sy;

                // Pointer-form store avoids a slower address-lowering path in
                // current Apple Metal compilers for this dequantized tile write.
                *(sa + 64*ib + 8*ly + lx) = temp_a[i/4][i%4];
            }
        }

        if (FC_mul_mm_bc_inp) {
            for (short i = 0; i < 8; ++i) {
                const short sx = (tiitg%NL1);
                const short sy = (tiitg/NL1)/8;

                const short lx = i;
                const short ly = (tiitg/NL1)%8;

                const short ib = 4*sx + sy;
                *(sb + 64*ib + 8*ly + lx) = loop_k + iy + i < args.ne00 ? (S1) *((device T1 *) y + i) : 0;
            }
        } else {
            const short sx = (tiitg%NL1);
            const short sy = (tiitg/NL1)/8;

            const short ly = (tiitg/NL1)%8;

            const short ib = 4*sx + sy;
            *(threadgroup S1_2x4 *)(sb + 64*ib + 8*ly) = (S1_2x4)(*((device T1_2x4 *) y));
        }

        il = (il + 2 < nl) ? il + 2 : il % 2;
        x  = (il < 2) ? x + (2 + nl - 1)/nl : x;

        y += NK;

        threadgroup_barrier(mem_flags::mem_threadgroup);

        // load matrices from threadgroup memory and conduct outer products
        threadgroup const S0 * lsma = (sa + 4*64*(sgitg%2));
        threadgroup const S1 * lsmb = (sb + 2*64*(sgitg/2));

        FOR_UNROLL (short ik = 0; ik < NK/8; ik++) {
            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 4; i++) {
                simdgroup_load(ma[i], lsma + 64*i, 8, 0, false);
            }

            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 2; i++) {
                simdgroup_load(mb[i], lsmb + 64*i, 8, 0, false);
            }

            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 8; i++){
                simdgroup_multiply_accumulate(mc[i], mb[i/4], ma[i%4], mc[i]);
            }

            lsma += 8*64;
            lsmb += 4*64;
        }
    }

    if (!FC_mul_mm_bc_out || (r0 + NR0 <= args.ne0 && r1 + NR1 <= args.ne1)) {
        // if no bounds checks on the output are needed, we can directly write to device memory
        device float * C = (device float *) dst +
            (r0 + 32*(sgitg &  1)) + \
            (r1 + 16*(sgitg >> 1)) * args.ne0 + im*args.ne1*args.ne0;

        for (short i = 0; i < 8; i++) {
            simdgroup_store(mc[i], C + 8*(i%4) + 8*args.ne0*(i/4), args.ne0, 0, false);
        }
    } else {
        // block is smaller than 64x32, we should avoid writing data outside of the matrix
        threadgroup_barrier(mem_flags::mem_threadgroup);

        threadgroup float * temp_str = ((threadgroup float *) shmem) + 32*(sgitg&1) + (16*(sgitg >> 1))*NR0;

        for (short i = 0; i < 8; i++) {
            simdgroup_store(mc[i], temp_str + 8*(i%4) + 8*NR0*(i/4), NR0, 0, false);
        }

        threadgroup_barrier(mem_flags::mem_threadgroup);

        if (sgitg == 0) {
            for (int j = tiitg; j < nr1; j += NR1) {
                device float  * D  = (device float  *) dst + r0 + (r1 + j)*args.ne0 + im*args.ne1*args.ne0;
                device float4 * D4 = (device float4 *) D;

                threadgroup float  * C  = temp_str + (j*NR0);
                threadgroup float4 * C4 = (threadgroup float4 *) C;

                int i = 0;
                for (; i < nr0/4; i++) {
                    *(D4 + i) = *(C4 + i);
                }

                i *= 4;
                for (; i < nr0; i++) {
                    *(D + i) = *(C + i);
                }
            }
        }
    }
}

// Legacy F16-weight/F32-RHS prefill matmul with a per-row RMS scale applied at
// the existing F32-to-F16 RHS staging boundary.  The tile layout, half inputs,
// float accumulators, and output path intentionally mirror kernel_mul_mm so the
// only arithmetic change versus materializing RMSNorm first is where x*scale is
// rounded from F32 to F16.
kernel void kernel_mul_mm_f16_f32_scaled(
        constant q36_metal_args_mul_mm & args,
        device const char * src0,
        device const char * src1,
        device       char * dst,
        device const float * scales,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiitg[[thread_index_in_threadgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {

    threadgroup half * sa = (threadgroup half *)(shmem);
    threadgroup half * sb = (threadgroup half *)(shmem + 4096);

    constexpr int NR0 = 64;
    constexpr int NR1 = 32;

    constexpr int NK  = 32;
    constexpr int NL0 = NK/16;
    constexpr int NL1 = NK/8;

    const int im = tgpig.z;
    const int r0 = tgpig.y*NR0;
    const int r1 = tgpig.x*NR1;

    // if this block is of 64x32 shape or smaller
    const short nr0 = (args.ne0 - r0 < NR0) ? (args.ne0 - r0) : NR0;
    const short nr1 = (args.ne1 - r1 < NR1) ? (args.ne1 - r1) : NR1;

    // a thread shouldn't load data outside of the matrix
    const short lr0 = ((short)tiitg/NL0) < nr0 ? ((short)tiitg/NL0) : nr0 - 1; // 0 .. 63
    const short lr1 = ((short)tiitg/NL1) < nr1 ? ((short)tiitg/NL1) : nr1 - 1; // 0 .. 31

    const short il0 = (tiitg % NL0);

    short il = il0;

    const int i12 = im%args.ne12;
    const int i13 = im/args.ne12;

    const uint64_t offset0 = (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;
    const short    offset1 = il0;

    device const half4x4 * x = (device const half4x4 *)(src0 + args.nb01*(r0 + lr0) + offset0) + offset1;

    const short iy = 8*(tiitg % NL1);

    device const float * y = (device const float *)(src1
        + args.nb13*i13
        + args.nb12*i12
        + args.nb11*(r1 + lr1)
        + args.nb10*iy);
    const float row_scale = scales[r1 + lr1];

    simdgroup_half8x8 ma[4];
    simdgroup_half8x8 mb[2];

    simdgroup_float8x8 mc[8];

    for (short i = 0; i < 8; i++){
        mc[i] = make_filled_simdgroup_matrix<float, 8>(0.f);
    }

    for (int loop_k = 0; loop_k < args.ne00; loop_k += NK) {
        // load data and store to threadgroup memory
        if (FC_mul_mm_bc_inp) {
            threadgroup_barrier(mem_flags::mem_threadgroup);

            // no need for dequantization
            for (short i = 0; i < 16; i++) {
                const short sx = 2*il0 + i/8;
                const short sy = (tiitg/NL0)/8;

                const short lx = (tiitg/NL0)%8;
                const short ly = i%8;

                const short ib = 8*sx + sy;

                *(sa + 64*ib + 8*ly + lx) = loop_k + 16*il + i < args.ne00 ? *((device half *) x + i) : 0;
            }
        } else {
            half4x4 temp_a;
            dequantize_f16(x, il, temp_a);

            threadgroup_barrier(mem_flags::mem_threadgroup);

            FOR_UNROLL (short i = 0; i < 16; i++) {
                const short sx = 2*il0 + i/8;
                const short sy = (tiitg/NL0)/8;

                const short lx = (tiitg/NL0)%8;
                const short ly = i%8;

                const short ib = 8*sx + sy;

                // Pointer-form store matches the legacy F16 tile layout.
                *(sa + 64*ib + 8*ly + lx) = temp_a[i/4][i%4];
            }
        }

        if (FC_mul_mm_bc_inp) {
            for (short i = 0; i < 8; ++i) {
                const short sx = (tiitg%NL1);
                const short sy = (tiitg/NL1)/8;

                const short lx = i;
                const short ly = (tiitg/NL1)%8;

                const short ib = 4*sx + sy;
                const float scaled = loop_k + iy + i < args.ne00 ? y[i] * row_scale : 0.0f;

                *(sb + 64*ib + 8*ly + lx) = (half)scaled;
            }
        } else {
            const short sx = (tiitg%NL1);
            const short sy = (tiitg/NL1)/8;

            const short ly = (tiitg/NL1)%8;

            const short ib = 4*sx + sy;

            const float2x4 raw = *((device const float2x4 *) y);
            float2x4 scaled;
            scaled[0] = raw[0] * row_scale;
            scaled[1] = raw[1] * row_scale;
            *(threadgroup half2x4 *)(sb + 64*ib + 8*ly) = (half2x4)scaled;
        }

        il = (il + 2 < 1) ? il + 2 : il % 2;
        x  = (il < 2) ? x + 2 : x;

        y += NK;

        threadgroup_barrier(mem_flags::mem_threadgroup);

        // load matrices from threadgroup memory and conduct outer products
        threadgroup const half * lsma = (sa + 4*64*(sgitg%2));
        threadgroup const half * lsmb = (sb + 2*64*(sgitg/2));

        FOR_UNROLL (short ik = 0; ik < NK/8; ik++) {
            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 4; i++) {
                simdgroup_load(ma[i], lsma + 64*i, 8, 0, false);
            }

            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 2; i++) {
                simdgroup_load(mb[i], lsmb + 64*i, 8, 0, false);
            }

            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 8; i++){
                simdgroup_multiply_accumulate(mc[i], mb[i/4], ma[i%4], mc[i]);
            }

            lsma += 8*64;
            lsmb += 4*64;
        }
    }

    if (!FC_mul_mm_bc_out || (r0 + NR0 <= args.ne0 && r1 + NR1 <= args.ne1)) {
        // if no bounds checks on the output are needed, we can directly write to device memory
        device float * C = (device float *) dst +
            (r0 + 32*(sgitg &  1)) + \
            (r1 + 16*(sgitg >> 1)) * args.ne0 + im*args.ne1*args.ne0;

        for (short i = 0; i < 8; i++) {
            simdgroup_store(mc[i], C + 8*(i%4) + 8*args.ne0*(i/4), args.ne0, 0, false);
        }
    } else {
        // block is smaller than 64x32, we should avoid writing data outside of the matrix
        threadgroup_barrier(mem_flags::mem_threadgroup);

        threadgroup float * temp_str = ((threadgroup float *) shmem) + 32*(sgitg&1) + (16*(sgitg >> 1))*NR0;

        for (short i = 0; i < 8; i++) {
            simdgroup_store(mc[i], temp_str + 8*(i%4) + 8*NR0*(i/4), NR0, 0, false);
        }

        threadgroup_barrier(mem_flags::mem_threadgroup);

        if (sgitg == 0) {
            for (int j = tiitg; j < nr1; j += NR1) {
                device float  * D  = (device float  *) dst + r0 + (r1 + j)*args.ne0 + im*args.ne1*args.ne0;
                device float4 * D4 = (device float4 *) D;

                threadgroup float  * C  = temp_str + (j*NR0);
                threadgroup float4 * C4 = (threadgroup float4 *) C;

                int i = 0;
                for (; i < nr0/4; i++) {
                    *(D4 + i) = *(C4 + i);
                }

                i *= 4;
                for (; i < nr0; i++) {
                    *(D + i) = *(C + i);
                }
            }
        }
    }
}

kernel void kernel_mul_mm_f16_f32_pair(
        constant q36_metal_args_mul_mm & args,
        device const char * src0_a,
        device const char * src0_b,
        device const char * src1,
        device       char * dst_a,
        device       char * dst_b,
        threadgroup  char * shmem [[threadgroup(0)]],
        uint3  tgpig[[threadgroup_position_in_grid]],
        ushort tiitg[[thread_index_in_threadgroup]],
        ushort sgitg[[simdgroup_index_in_threadgroup]]) {
    threadgroup half * sa_a = (threadgroup half *)(shmem);
    threadgroup half * sa_b = (threadgroup half *)(shmem + 4096);
    threadgroup half * sb   = (threadgroup half *)(shmem + 8192);

    constexpr int NR0 = 64;
    constexpr int NR1 = 32;
    constexpr int NK  = 32;
    constexpr int NL0 = NK/16;
    constexpr int NL1 = NK/8;

    const int im = tgpig.z;
    const int r0 = tgpig.y*NR0;
    const int r1 = tgpig.x*NR1;

    const short nr0 = (args.ne0 - r0 < NR0) ? (args.ne0 - r0) : NR0;
    const short nr1 = (args.ne1 - r1 < NR1) ? (args.ne1 - r1) : NR1;

    const short lr0 = ((short)tiitg/NL0) < nr0 ? ((short)tiitg/NL0) : nr0 - 1;
    const short lr1 = ((short)tiitg/NL1) < nr1 ? ((short)tiitg/NL1) : nr1 - 1;

    const short il0 = (tiitg % NL0);
    short il = il0;

    const int i12 = im%args.ne12;
    const int i13 = im/args.ne12;

    const uint64_t offset0 = (i12/args.r2)*args.nb02 + (i13/args.r3)*args.nb03;
    const short    offset1 = il0;

    device const half4x4 * xa = (device const half4x4 *)(src0_a + args.nb01*(r0 + lr0) + offset0) + offset1;
    device const half4x4 * xb = (device const half4x4 *)(src0_b + args.nb01*(r0 + lr0) + offset0) + offset1;

    const short iy = 8*(tiitg % NL1);

    device const float * y = (device const float *)(src1
        + args.nb13*i13
        + args.nb12*i12
        + args.nb11*(r1 + lr1)
        + args.nb10*iy);

    simdgroup_half8x8 ma[4];
    simdgroup_half8x8 mb[2];

    simdgroup_float8x8 mc_a[8];
    simdgroup_float8x8 mc_b[8];

    for (short i = 0; i < 8; i++) {
        mc_a[i] = make_filled_simdgroup_matrix<float, 8>(0.f);
        mc_b[i] = make_filled_simdgroup_matrix<float, 8>(0.f);
    }

    for (int loop_k = 0; loop_k < args.ne00; loop_k += NK) {
        half4x4 temp_a;
        half4x4 temp_b;
        dequantize_f16(xa, il, temp_a);
        dequantize_f16(xb, il, temp_b);

        threadgroup_barrier(mem_flags::mem_threadgroup);

        FOR_UNROLL (short i = 0; i < 16; i++) {
            const short sx = 2*il0 + i/8;
            const short sy = (tiitg/NL0)/8;

            const short lx = (tiitg/NL0)%8;
            const short ly = i%8;

            const short ib = 8*sx + sy;

            *(sa_a + 64*ib + 8*ly + lx) = temp_a[i/4][i%4];
            *(sa_b + 64*ib + 8*ly + lx) = temp_b[i/4][i%4];
        }

        if (FC_mul_mm_bc_inp) {
            for (short i = 0; i < 8; ++i) {
                const short sx = (tiitg%NL1);
                const short sy = (tiitg/NL1)/8;

                const short lx = i;
                const short ly = (tiitg/NL1)%8;

                const short ib = 4*sx + sy;

                *(sb + 64*ib + 8*ly + lx) = loop_k + iy + i < args.ne00 ? (half) *((device float *) y + i) : 0;
            }
        } else {
            const short sx = (tiitg%NL1);
            const short sy = (tiitg/NL1)/8;

            const short ly = (tiitg/NL1)%8;

            const short ib = 4*sx + sy;

            *(threadgroup half2x4 *)(sb + 64*ib + 8*ly) = (half2x4)(*((device float2x4 *) y));
        }

        il = (il + 2 < 1) ? il + 2 : il % 2;
        xa = (il < 2) ? xa + 2 : xa;
        xb = (il < 2) ? xb + 2 : xb;

        y += NK;

        threadgroup_barrier(mem_flags::mem_threadgroup);

        threadgroup const half * lsma_a = (sa_a + 4*64*(sgitg%2));
        threadgroup const half * lsma_b = (sa_b + 4*64*(sgitg%2));
        threadgroup const half * lsmb   = (sb   + 2*64*(sgitg/2));

        FOR_UNROLL (short ik = 0; ik < NK/8; ik++) {
            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 2; i++) {
                simdgroup_load(mb[i], lsmb + 64*i, 8, 0, false);
            }

            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 4; i++) {
                simdgroup_load(ma[i], lsma_a + 64*i, 8, 0, false);
            }

            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 8; i++) {
                simdgroup_multiply_accumulate(mc_a[i], mb[i/4], ma[i%4], mc_a[i]);
            }

            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 4; i++) {
                simdgroup_load(ma[i], lsma_b + 64*i, 8, 0, false);
            }

            simdgroup_barrier(mem_flags::mem_none);

            FOR_UNROLL (short i = 0; i < 8; i++) {
                simdgroup_multiply_accumulate(mc_b[i], mb[i/4], ma[i%4], mc_b[i]);
            }

            lsma_a += 8*64;
            lsma_b += 8*64;
            lsmb   += 4*64;
        }
    }

    if (!FC_mul_mm_bc_out || (r0 + NR0 <= args.ne0 && r1 + NR1 <= args.ne1)) {
        device float * C_a = (device float *) dst_a +
            (r0 + 32*(sgitg &  1)) +
            (r1 + 16*(sgitg >> 1)) * args.ne0 + im*args.ne1*args.ne0;
        device float * C_b = (device float *) dst_b +
            (r0 + 32*(sgitg &  1)) +
            (r1 + 16*(sgitg >> 1)) * args.ne0 + im*args.ne1*args.ne0;

        for (short i = 0; i < 8; i++) {
            simdgroup_store(mc_a[i], C_a + 8*(i%4) + 8*args.ne0*(i/4), args.ne0, 0, false);
            simdgroup_store(mc_b[i], C_b + 8*(i%4) + 8*args.ne0*(i/4), args.ne0, 0, false);
        }
    } else {
        threadgroup_barrier(mem_flags::mem_threadgroup);

        threadgroup float * temp_str = (threadgroup float *) shmem;

        for (short i = 0; i < 8; i++) {
            simdgroup_store(mc_a[i],
                            temp_str + 32*(sgitg&1) + (16*(sgitg >> 1))*NR0 + 8*(i%4) + 8*NR0*(i/4),
                            NR0,
                            0,
                            false);
        }

        threadgroup_barrier(mem_flags::mem_threadgroup);

        if (sgitg == 0) {
            for (int j = tiitg; j < nr1; j += NR1) {
                device float  * D  = (device float *) dst_a + r0 + (r1 + j)*args.ne0 + im*args.ne1*args.ne0;
                device float4 * D4 = (device float4 *) D;

                threadgroup float  * C  = temp_str + (j*NR0);
                threadgroup float4 * C4 = (threadgroup float4 *) C;

                int i = 0;
                for (; i < nr0/4; i++) {
                    *(D4 + i) = *(C4 + i);
                }

                i *= 4;
                for (; i < nr0; i++) {
                    *(D + i) = *(C + i);
                }
            }
        }

        threadgroup_barrier(mem_flags::mem_threadgroup);

        for (short i = 0; i < 8; i++) {
            simdgroup_store(mc_b[i],
                            temp_str + 32*(sgitg&1) + (16*(sgitg >> 1))*NR0 + 8*(i%4) + 8*NR0*(i/4),
                            NR0,
                            0,
                            false);
        }

        threadgroup_barrier(mem_flags::mem_threadgroup);

        if (sgitg == 0) {
            for (int j = tiitg; j < nr1; j += NR1) {
                device float  * D  = (device float *) dst_b + r0 + (r1 + j)*args.ne0 + im*args.ne1*args.ne0;
                device float4 * D4 = (device float4 *) D;

                threadgroup float  * C  = temp_str + (j*NR0);
                threadgroup float4 * C4 = (threadgroup float4 *) C;

                int i = 0;
                for (; i < nr0/4; i++) {
                    *(D4 + i) = *(C4 + i);
                }

                i *= 4;
                for (; i < nr0; i++) {
                    *(D + i) = *(C + i);
                }
            }
        }
    }
}

typedef decltype(kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, float4x4, 1, dequantize_f32, float, float4x4, float, float2x4>) mul_mm_t;
typedef decltype(kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, float4x4, 1, dequantize_f32, float, float4x4, half, half2x4>) mul_mm_f16rhs_t;
typedef decltype(kernel_mul_mm<float, float4x4, simdgroup_float8x8, float, float2x4, simdgroup_float8x8, float4x4, 1, dequantize_f32, float, float4x4, float, float2x4>) mul_mm_ff32_t;

// Host-visible prefill matmul variants for F32, F16, and Q8_0 weights.
template [[host_name("kernel_mul_mm_f32_ff32")]] kernel mul_mm_ff32_t kernel_mul_mm<float, float4x4, simdgroup_float8x8, float, float2x4, simdgroup_float8x8, float4x4, 1, dequantize_f32, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_f16_f32")]]  kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, half4x4, 1, dequantize_f16,  half,  half4x4,  float, float2x4>;
template [[host_name("kernel_mul_mm_q8_0_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, block_q8_0, 2, dequantize_q8_0, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_q4_0_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q4_0, 2, dequantize_dense_q4_0, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_q2_K_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q2_K, 16, dequantize_dense_q2_K, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_q3_K_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q3_K, 16, dequantize_dense_q3_K, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_q4_K_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q4_K, 16, dequantize_dense_q4_K, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_q5_K_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q5_K, 16, dequantize_dense_q5_K, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_q6_K_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q6_K, 16, dequantize_dense_q6_K, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_iq2_xxs_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq2_xxs, 16, dequantize_dense_iq2_xxs, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_iq2_xs_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq2_xs, 16, dequantize_dense_iq2_xs, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_iq2_s_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq2_s, 16, dequantize_dense_iq2_s, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_iq1_s_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq1_s, 16, dequantize_dense_iq1_s, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_iq3_xxs_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq3_xxs, 16, dequantize_dense_iq3_xxs, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_iq3_s_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq3_s, 16, dequantize_dense_iq3_s, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_iq4_nl_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq4_nl, 16, dequantize_dense_iq4_nl, float, float4x4, float, float2x4>;
template [[host_name("kernel_mul_mm_iq4_xs_f32")]] kernel mul_mm_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq4_xs, 16, dequantize_dense_iq4_xs, float, float4x4, float, float2x4>;

// Dense prefill converts one RHS activation panel to F16 and shares it across
// all output tiles and paired projections, mirroring Vulkan's shared panel.
template [[host_name("kernel_mul_mm_q4_0_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q4_0, 2, dequantize_dense_q4_0, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_q2_K_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q2_K, 16, dequantize_dense_q2_K, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_q3_K_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q3_K, 16, dequantize_dense_q3_K, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_q4_K_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q4_K, 16, dequantize_dense_q4_K, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_q5_K_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q5_K, 16, dequantize_dense_q5_K, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_q6_K_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_q6_K, 16, dequantize_dense_q6_K, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_iq2_xxs_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq2_xxs, 16, dequantize_dense_iq2_xxs, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_iq2_xs_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq2_xs, 16, dequantize_dense_iq2_xs, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_iq2_s_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq2_s, 16, dequantize_dense_iq2_s, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_iq1_s_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq1_s, 16, dequantize_dense_iq1_s, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_iq3_xxs_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq3_xxs, 16, dequantize_dense_iq3_xxs, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_iq3_s_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq3_s, 16, dequantize_dense_iq3_s, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_iq4_nl_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq4_nl, 16, dequantize_dense_iq4_nl, float, float4x4, half, half2x4>;
template [[host_name("kernel_mul_mm_iq4_xs_f16")]] kernel mul_mm_f16rhs_t kernel_mul_mm<half, half4x4, simdgroup_half8x8, half, half2x4, simdgroup_half8x8, q36_dense_block_iq4_xs, 16, dequantize_dense_iq4_xs, float, float4x4, half, half2x4>;
